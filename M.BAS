10 Rem @@@@@@@@@@ (N=N+1)====[N=N MOD RANGE+1]
20 Rem $STATIC
30 Rem ON A GOTO 1740,2380,3050,3150 'HAVE ERASE/REDIM/DIM STATEMENTS
40 Clear: Screen 0, , 0, 0: Color , 0: Cls: Width 80: Randomize Timer: Locate , , 0: KEY Off: Def Seg = 64: If (Peek(16) And &H30) = &H30 Then ZZ = 7 Else ZZ = 8
50 Def Seg = 0
60 Dim J(4), PROPERTY_SHORT_NAMES$((40), C(40), FC(40), BC(40), O(40), K(40), G(10, 4), GN(40), RENT_RATES(6, 40), L$(4), L(4), CP(4), X(2, 4), Y(4), B(4, 11), U(40), PP(4, 28), P(4), M(40), C1(16), C2(16), BN(40), PM(40), W(4), BH(4), V(4), H(4), PC(4), PROPERTY_NAMES$(40), FJ(4), I(4), CH$(32), RTP(4), PD(40)
70 Dim CM(40), CU(40), CI(40), CW(40), CO(40), KR(240), C3(11), TP(4), BV(4), PPN(40), CC3(4), CCR(4), GN$(60), KY$(11), HB(10), O$(8), OO$(8), OO(8), OCCR(4), OCC3(4), CARD$(2, 16, 7), N(4), UN(4), XN(4), J$(4)

Rem Load data file: property, cards, etc
80 Open "I", #1, "M.DF"

Rem Load 41 properties (Go is loaded twice (first and last) for some reason)
For P = 0 To 40

Rem Load property name, abbreviation, foreground color, background color, group num (property group#), kind (board space type), board num (side# of board) (1-4 or 0 - why 0?), cost
Input #1, PROPERTY_NAMES$(P), PROPERTY_SHORT_NAMES$((P), FC(P), BC(P), GN(P), K(P), BN(P), C(P): Next

Rem Load 6 rent rates x 41 properties
90 For A = O To 5
    For P = 0 To 40
        Input #1, RENT_RATES(A, P)
Next P, A

Rem Load 11 ? x 4 ?s
For G = 0 To 10
    For P = 1 To 4
        Input #1, G(G, P), B(P, G)
Next P, G

Rem Load 240 out of 256 character #s (why?)
For A = 1 To 240
    Input #1, KR(A)
Next A

Rem Load 2 card types (chance and community chest) x 16 cards x # of text lines
100 For A = 1 To 2
    For B = 1 To 16
        Input #1, CARD$(A, B, 0)
        110 For C = 1 To Val(CARD$(A, B, 0))
            Input #1, CARD$(A, B, C)
Next C, B, A
Close #1

120 PLAYY = 0: SOUNDS = 1: AUTOKYBD = 0: CP = 0: CL = 1: PL = 1: E = 0: T = 0: FM = 1: NUM_OF_PLAYERS = 4: GoSub 8570
130 LB = 0: C# = 0: For C3 = 2 To 15: If C3 = 3 Or C3 = 6 Or C3 = 7 Or C3 = 8 Or C3 = 14 Or C3 = 15 Then 140 Else C# = C# + 1: C3(C#) = C3
140 Next C3: For L = 1 To 4: N(L) = L: UN(L) = L: Next L
150 KK = 8: For L = 1 To 4: CCR(N(L)) = N(L): OCCR(N(L)) = N(L): L(N(L)) = KR(N(L)): CC3(N(L)) = N(L): OCC3(N(L)) = N(L): PC(N(L)) = C3(N(L)): BV(N(L)) = 11: TP(N(L)) = 0: RTP(N(L)) = 0: CP(N(L)) = 0: X(1, N(L)) = R1 - KK + N(L): X(2, N(L)) = L1 + KK - N(L): Y(N(L)) = T1 + 4: W(N(L)) = 1500
151 J$(L) = "Player" + Right$(Str$(L), 1): Next L: WW = 3: WH = 9
160 CGN$ = "": L = 1: B = 0: Randomize Timer: For A = 1 To 16: C1(A) = A: C2(A) = A: Next A: For A = 1 To 16: Swap C1(A), C1(Int(Rnd * 16 + 1)): Swap C2(A), C2(Int(Rnd * 16 + 1)): Next A: GoSub 750: GoTo 900
170 Rem
180 Rem chrs/colors
190 Rem
200 Screen 0, , 1, 0: Color 0, 0: Cls: Screen 0, , 1, 1
210 WDTH = 80 / C#: V = 11: H = 2: EH = H + C# * WDTH: BH = H
220 H = 1: For C3 = 1 To C#: Locate V - 1, H + 4: Color ZZ, 0: Print C3: Locate V, H: Color C3(C3), 0: Print String$(WDTH, Chr$(219));: H = H + WDTH: Next C3: Color ZZ, 0: H = 2: BH = H: EH = 79: V = 13: BV = V: EV = V + 10
230 Locate BV, BH: For CR = 1 To 6: Print Chr$(CR); " ";: Next CR: Print Chr$(8): H = 16
240 For CR = 14 To 27: Locate V, H: Print Chr$(CR);: H = H + 2: Next CR
250 Locate BV, BH + 42: Print "! "; Chr$(34); " # $ % & ' ( ) * + , - . / 0 1 2 3"
260 Locate BV + 2, BH: Print "4 5 6 7 8 9 : ; < = > ? @ A B C D E F G H I J K L M N O P Q R S T U V W X Y Z ["
270 Locate BV + 4, BH: Print "\ ] ^ _ ` a b c d e f g h i j k l m n o p q r s t u v w x y z { | } ~  €  ‚ ƒ"
280 Locate BV + 6, BH: Print "„ … † ‡ ˆ ‰ Š ‹ Œ  Ž   ‘ ’ “ ” • – — ˜ ™ š › œ  ž Ÿ   ¡ ¢ £ ¤ ¥ ¦ § ¨ © ª «"
290 Locate BV + 8, BH: Print "¬ ­ ® ¯ ° ± ² ´ µ ¶ · ¸ ¹ º » ¼ ½ ¾ ¿ À Á Â Ã Ä Å Æ Ç È É Ê Ë Ì Í Î Ï Ð Ñ Ò Ó Ô"
300 Locate BV + 10, BH: Print "Õ Ö × Ø Ù Ú Û Ü Ý à á â ã ä å æ ç è é ê ë ì í î ï ð ñ ò ó ô õ ö ÷ ø ù ú û ü ý þ";
310 H = 2: BH = H: EH = BH + 78: V = 13: BV = V: EV = V + 10
320 For L = 1 To 4: CC3(N(L)) = OCC3(N(L)): CCR(N(L)) = OCCR(N(L)): Next L
330 For L = 1 To 4: H(N(L)) = BH + (CCR(N(L)) - (Int(CCR(N(L)) / 40) * 40) - 1) * 2: V(N(L)) = BV + Int(CCR(N(L)) / 40) * 2: Next L
340 For L = 1 To NUM_OF_PLAYERS: Locate V(N(L)), H(N(L)): Color C3(CC3(N(L))), 0: Print Chr$(KR(CCR(N(L)))): Next L
350 Poke 1050, Peek(1052): L = 1: Y1 = 7: X1 = 40: CC = ZZ: X1 = 28
360 Rem
370 Rem *** menu ***
380 X1 = (80 - 4 * NUM_OF_PLAYERS) / 2
390 Locate Y1, 1: Color ZZ, 0: Print Space$(X1) + " [ ";
400 For A = 1 To NUM_OF_PLAYERS - 1: Color C3(CC3(N(A))), 0: Print Chr$(KR(CCR(N(A))));: Color ZZ, 0: Print " | ";: Next A
410 Color C3(CC3(N(NUM_OF_PLAYERS))), 0: Print Chr$(KR(CCR(N(NUM_OF_PLAYERS))));: Color ZZ, 0: Print " ]" + Space$((80 - 4 * NUM_OF_PLAYERS - X1) / 2)
420 GoTo 480
430 Locate Y1 - 1, X1 + 4 * LCL - 1: Print Chr$(218); Chr$(196); Chr$(191);
440 Locate Y1, X1 + 4 * LCL + 1: Print Chr$(179);
450 Locate Y1, X1 + 4 * LCL - 1: Print Chr$(179);
460 Locate Y1 + 1, X1 + 4 * LCL - 1: Print Chr$(192); Chr$(196); Chr$(217);
470 Return
480 Color 0, 0: GoSub 430: Color 14, 0: LCL = L: GoSub 430
490 Color C3(CC3(N(L))), 0: Locate V(N(L)), H(N(L)): Print Chr$(KR(CCR(N(L))));: Locate Y1, X1 + 4 * L: Print Chr$(KR(CCR(N(L))));
500 A$ = InKey$: On Len(A$) + 1 GOTO 500, 510, 570
510 If A$ = Chr$(13) Then For F = 1 To 4: OCC3(N(F)) = CC3(N(F)): OCCR(N(F)) = CCR(N(F)): Next F: AA = 1: GoTo 720
520 If A$ = Chr$(27) Then For F = 1 To 4: CC3(N(F)) = OCC3(N(F)): CCR(N(F)) = OCCR(N(F)): Next F: AA = 0: GoTo 730
530 GoTo 540
540 IF VAL(A$)>C# OR VAL(A$)=0 THEN 560 ELSE FOR A=1 TO NUM_OF_PLAYERS:IF VAL(A$)=CC3(N(A)) THEN CHCK=1
550 NEXT A:IF CHCK THEN CHCK=0:GOTO 490 ELSE CC3(N(L))=VAL(A$):GOTO 490
560 IF A$=CHR$(9) THEN LL=L:L=L+1:GOTO 580 ELSE 500
570 A$=RIGHT$(A$,1):IF A$=CHR$(15) THEN LL=L:L=L-1 ELSE 600
580 IF L>NUM_OF_PLAYERS THEN L=1 ELSE IF L<1 THEN L=NUM_OF_PLAYERS
590 GOTO 480
600 IF A$=CHR$(77) THEN VV=0:HV=2:CV=1:GOTO 640
610 IF A$=CHR$(75) THEN VV=0:HV=-2:CV=-1:GOTO 640
620 IF A$=CHR$(72) THEN HV=0:VV=-2:CV=-40:GOTO 640
630 IF A$=CHR$(80) THEN HV=0:VV=2:CV=40 ELSE 500
640 COLOR ZZ,0:LOCATE V(N(L)),H(N(L)):PRINT CHR$(KR(CCR(N(L))));
650 CCR(N(L))=CCR(N(L))+CV:V(N(L))=V(N(L))+VV:H(N(L))=H(N(L))+HV
660 IF H(N(L))<BH THEN H(N(L))=EH:CCR(N(L))=CCR(N(L))+40:GOTO 700
670 IF H(N(L))>EH THEN H(N(L))=BH:CCR(N(L))=CCR(N(L))-40:GOTO 700
680 IF V(N(L))>EV THEN V(N(L))=BV:CCR(N(L))=CCR(N(L))-240:GOTO 700
690 IF V(N(L))<BV THEN V(N(L))=EV:CCR(N(L))=CCR(N(L))+240
700 FOR A=1 TO NUM_OF_PLAYERS:IF CCR(A)=CCR(N(L)) AND L<>A THEN CHECK=1
710 NEXT A:IF CHECK THEN CHECK=0:GOTO 650 ELSE 490
720 FOR L=1 TO NUM_OF_PLAYERS:L(N(L))=KR(CCR(N(L))):PC(N(L))=C3(CC3(N(L))):NEXT L
730 IF AA=1 THEN CLS:SCREEN 0,,0,1:COLOR 0,0:CLS
740 SCREEN 0,,0,0:RETURN
750 POKE 1050,PEEK(1052):COLOR ZZ,0:LOCATE ,,0,7,0:CLS:LOCATE T1,L1:PRINT"ÇÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÂÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄ¶"
760 LOCATE T1+1,L1:PRINT"º         ³     ³     ³     ³     ³     ³     ³     ³     ³     ³         º"
770 LOCATE T1+2,L1:PRINT"º         ³     ³     ³     ³     ³     ³     ³     ³     ³     ³         º"
780 LOCATE T1+3,L1:PRINT"º         ³     ³     ³     ³     ³";:COLOR 7:PRINT" ÜÛ";:COLOR ZZ:PRINT" ³     ³     ³     ³     ³         º"
790 LOCATE T1+4,L1:PRINT"º         ³     ³     ³     ³     ³";:COLOR 7:PRINT" ø ø";:COLOR ZZ:PRINT" ³     ³     ³     ³     ³         º"
800 LOCATE T1+5,L1:PRINT"ÈÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¼":COLOR ZZ,0:GOSUB 8570
810 GOSUB 11530:GOSUB 11610:GOSUB 11570
820 LOCATE T2,L2:PRINT"ÉÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ»"
830 FOR A=1 TO 16:LOCATE T2+A,L2:PRINT"º";:LOCATE ,R2:PRINT"º";:NEXT A
840 LOCATE B2,L2:PRINT"ÈÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼";
850 FOR L=1 TO NUM_OF_PLAYERS:COLOR PC(N(L)),0:LOCATE T6,BH(L)-1:PRINT"ÉÍÍÍÍÍÍÍÍÍ»"
860 FOR V=T6+1 TO BV(L)+W#-1:LOCATE V,BH(L)-1:PRINT"º";:LOCATE V,BH(L)+W6:PRINT "º";:NEXT V
870 GOSUB 1510 'LOCATE T6+2,BH(L)-1:PRINT CHR$(199);STRING$(9,196);CHR$(182)
880 LOCATE BV(L)+W#,BH(L)-1:PRINT"ÈÍÍÍÍÍÍÍÍÍ¼";:NEXT L:RETURN
890  REM 
900  REM   @@@@@@@@ MONOPOLY @@@@@@@@@@
910  REM
920 FOR L=1 TO NUM_OF_PLAYERS:H(N(L))=BH(N(L)):GOSUB 7240:GOSUB 1390:GOSUB 1540:NEXT L:GOSUB 1870
930 A$=INKEY$:IF LEN(A$) AND SG=0 THEN 940 ELSE IF SG THEN 950 ELSE 960
940 FOR P=1 TO 39:O(P)=INT((RND*NUM_OF_PLAYERS+1)*ABS(K(P)>1)):NEXT P
950 FOR L=1 TO NUM_OF_PLAYERS:GOSUB 1540:GOSUB 1600:GOSUB 1710:NEXT L
960  REM 
970  REM @@@@ DRAW BOARDS @@@@
980  REM 
990 IF CB THEN IF BW THEN SB=BN(PO)-1:GOTO 1010 ELSE SB=BN(PO):GOTO 1010
1000 GOSUB 12020:SB=BN(CP)
1010 GOSUB 1020:GOTO 1260
1020 IF SB=LB AND SG=0 THEN RETURN
1030 BX=R1-9:BXX=R1-9:BB=B(SB,0):IF LB<>SB THEN GOSUB 1230
1040 GOSUB 1060:GOSUB 1150:BXX=L1+1:BX=L1+2:BB=B(SB,10):GOSUB 1060:GOSUB 1240
1050 LB=SB:RETURN
1060 IF BB<>10 THEN COLOR ZZ,0:LOCATE T1,BX+1:PRINT STRING$(6,196):LOCATE T1+1,BX+1:PRINT SPACE$(6)
1070 ON BB/10+1 GOTO 1080,1090,1110,1120
1080 COLOR 4,0:LOCATE T1+2,BXX:PRINT"         ":LOCATE T1+3,BXX:PRINT "   "CHR$(27)"GO   ":GOTO 1130
1090 COLOR ZZ,0:LOCATE T1,BX+1:PRINT CHR$(194)+STRING$(4,196)+CHR$(194):LOCATE T1+1,BX+1:PRINT CHR$(179):LOCATE T1+1,BX+6:PRINT CHR$(179):LOCATE T1+2,BX+1:PRINT CHR$(192)+STRING$(4,196)+CHR$(217)
1100 COLOR 6,0:LOCATE T1+3,BX:PRINT"  JAIL  ":GOTO 1130
1110 COLOR 4,0:LOCATE T1+2,BXX:PRINT"  FREE   ":LOCATE T1+3,BXX:PRINT" PARKING ":GOTO 1130
1120 COLOR 11,0:LOCATE T1+2,BXX:PRINT"  GO TO  ":LOCATE T1+3,BXX:PRINT"  JAIL   ":GOTO 1130
1130 RETURN
1140 REM show properties
1150 BF=5:FOR P=1 TO 9:BX=R1-15-6*(P-1):BP=B(SB,P)
1160 COLOR 0,0:IF K(B(SB,P))=1 OR GN(BP)=10 THEN LOCATE T1+1,BX:PRINT"     ":LOCATE T1+3,BX:GOTO 1180 ELSE IF GN(BP)<>9 THEN LOCATE T1+3,BX:PRINT "     "
1170 LOCATE T1+1,BX
1180 IF GN(BP)<>10 THEN GOSUB 10880 ELSE COLOR ZZ-M(BP),0:PRINT " ";PROPERTY_SHORT_NAMES$((BP);" ";
1190  REM
1200 COLOR 2:LOCATE T1+2,(B(SB,10)-BP-1)*6+11+L1
1210 CU(BP)=U(BP):GOSUB 11050
1220 NEXT P:RETURN
1230 EB=1:CAB=LB:FOR CA=1 TO NUM_OF_PLAYERS:GOSUB 8520:NEXT CA:EB=0:RETURN
1240 FOR CA=1 TO NUM_OF_PLAYERS:IF CA<>G2 AND (BW=0 OR CA<>CL OR FM) THEN GOSUB 8510
1250 NEXT CA:RETURN
1260  REM 
1270 IF SG THEN SG=0:IF NM THEN FM=0:GOTO 11440
1280 IF CB THEN 4680
1290 IF GO2 THEN P1=CP:GOTO 4860
1300 GOSUB 11570:GOSUB 11530:IF DB THEN 1360
1310   REM 
1320   REM @@@@ CLEAR/SET ROUND VARIABLES @@@@
1330   REM 
1340 IF FM=0 THEN CL=CL+1:IF CL>NUM_OF_PLAYERS THEN CL=1 ELSE IF CL<1 THEN CL=NUM_OF_PLAYERS
1350 DB=0:PLAYY=0
1360 FM=0:CP=CP(N(CL)):PO=CP:LP=CP
1370 Y=Y(N(CL)):NY=Y:X=X(1,N(CL)):LX=X:FOR L=1 TO NUM_OF_PLAYERS:H(N(L))=BH(N(L)):NEXT L:RR=0:R=0
1380 GOSUB 1860:GOTO 1930
1390  REM 
1400  REM @@@@ SHOW PLAYER'S WEALTH @@@@
1410  REM 
1420 COLOR PC(N(L)),0
1430 IF W(N(L))<1000000! THEN LOCATE BV(L)-2,BH(L):PRINT USING "$$#####,";W(N(L)) ELSE LOCATE BV(L)-2,BH(L):PRINT USING"$$#######";W(N(L))
1440 RETURN
1450  REM
1460  REM @@@@ SHOW PLAYER'S EXCHANGE @@@@
1470  REM
1480 COLOR 14,1:IF EXC<100000! THEN LOCATE BV(L)-1,BH(L):PRINT USING "+$$####,";EXC;:PRINT " " ELSE LOCATE BV(L)-1,BH(L):PRINT USING"+$$#####,";EXC
1490 RETURN
1500  REM
1510  REM @@@@ UNDO PLAYER'S EXCHANGE @@@@
1520  REM
1521 LOCATE BV(L)-1,BH(L)-1:COLOR PC(N(L)),0:PSS=INT(1/2*WH-1/2*LEN(J$(N(L))))
1522 PRINT "Ç"+STRING$(PSS,CHR$(196))+J$(N(L));
1523 PSS=BH(L)+WH-POS(0):PRINT STRING$(PSS,CHR$(196))+"¶":RETURN
1530 COLOR PC(N(L)),0:LOCATE BV(L)-1,BH(L)-1:PRINT "ÇÄÄÄÄÄÄÄÄÄ¶":RETURN
1540  REM 
1550  REM @@@@ SHOW Bail CARDS @@@@
1560  REM 
1570 LOCATE BV(L)+W#,BH(L):IF J1=N(L) THEN COLOR 0,6:PRINT"Bail"; ELSE COLOR PC(N(L)),0:PRINT "ÍÍÍÍ";
1580 LOCATE BV(L)+W#,BH(L)+WH-4:IF J2=N(L) THEN COLOR 0,3:PRINT"Bail"; ELSE COLOR PC(N(L)),0:PRINT "ÍÍÍÍ";
1590 RETURN
1600  REM 
1610  REM @@@@ COUNT PLAYER'S PROPERTIES @@@@
1620  REM 
1630 H(L)=BH(L):V=BV(L)
1640 PP=0:FOR P=1 TO 39:IF O(P)<>N(L) OR GN(P)=>9 THEN 1660 ELSE IF RTP(N(L))=P THEN TP(N(L))=PP
1650 PP(N(L),PP)=P:PP=PP+1:PPN(P)=PP
1660 NEXT P
1670 FOR G=9 TO 10:FOR W=1 TO 4:P=G(G,W):IF O(P)<>N(L) THEN 1690 ELSE IF RTP(N(L))=P THEN TP(N(L))=PP
1680 PP(N(L),PP)=P:PP=PP+1:PPN(P)=PP
1690 NEXT W,G:P(N(L))=PP:IF P(N(L))<=14 THEN TP(N(L))=0:RTP(N(L))=PP(N(L),TP(N(L)))
1700 RETURN
1710 REM
1720 REM @@@@ SHOW COLUMN OF PROPERTIES @@@@
1730 REM
1740 ERASE PD:FOR A1=0 TO P(N(L))-1:A2=PP(N(L),A1):CM(A2)=M(A2):CO(A2)=O(A2):CU(A2)=U(A2):CI(A2)=0:NEXT A1:TP(N(L))=PPN(RTP(N(L)))-1:IF TP(N(L))<0 THEN TP(N(L))=0
1750 BF=0:PP=TP(N(L)):FOR V=BV(L) TO BV(L)+W#-1
1760 IF PP>P(N(L))-1 THEN IF P(N(L))>W# THEN PP=0 ELSE 1820
1770 IF PP=TP(N(L)) AND V>BV(L) THEN 1820 ELSE P=PP(N(L),PP)
1780 LOCATE V,H(L):BP=P:IF CO(P)<>O(P) THEN MPC=PC(CO(P)) ELSE MPC=ZZ-CM(P)
1790 GOSUB 10920:IF CU(P) THEN GOSUB 11050:GOTO 1810
1800 IF PD(P) THEN COLOR 14*(1+(PD(P)<0)),1:PRINT USING"####";PD(P); ELSE COLOR ,0:PRINT "    ";
1810 PP=PP+1
1820 NEXT V
1830 COLOR ,0:FOR A1=BV(L)+P(N(L)) TO BV(L)+W#-1
1840 LOCATE A1,BH(L):PRINT "         ";
1850 NEXT A1:IF DEAL THEN RETURN ELSE RETURN 'FOR A1=0 TO P(N(L))-1:A2=PP(N(L),A1):CM(A2)=0:CO(A2)=0:CU(A2)=0:NEXT A1:RETURN
1860  REM
1870  REM @@@@ SHOW HOUSES/HOTELS LEFT @@@@
1880  REM 
1890 CT=T:CE=E
1900 COLOR 2,0:LOCATE B2+1,L2:PRINT USING"##";32-CE;
1910 COLOR 4,0:LOCATE B2+1,L2+31:PRINT USING"##";12-CT;
1920 RETURN
1930   REM 
1940   REM @@@@ PLAYER OPTIONS @@@@
1950   REM
1960 PL=CL
1970 O$="":O$(1)="roll":O$(2)="deal":O$(3)="player":O$(4)="other":O=4
1980 IF NM=2 OR NM=3 OR NM=5 THEN OO(0)=0 ELSE OO(0)=1
1990 IF PL<>CL OR NM THEN OO(1)=0 ELSE OO(1)=1
2000 IF P(N(PL))=0 AND W(N(PL))=0 AND FJ(N(PL))=0 THEN OO(2)=0 ELSE OO(2)=1
2010 OO(3)=0:IF NUM_OF_PLAYERS>1 THEN FOR L=1 TO NUM_OF_PLAYERS:IF (P(N(L)) OR J(N(L)) OR W(N(L)) OR FJ(N(L))) AND L<>PL THEN OO(3)=1 ELSE NEXT L
2020 IF PL<>CL THEN OO(3)=1
2030 OO(4)=1:GOSUB 9940:ON AA+1 GOTO 2040,4300,11650,2060,10410
2040 IF NM THEN RETURN ELSE 1940
2050 REM
2060 REM SET Player
2070 IF NUM_OF_PLAYERS=2 THEN IF PL=1 THEN PL=2 ELSE PL=1 ELSE 2110
2080 GOTO 11670
2110 GOSUB 12161:IF EPN=0 THEN GOSUB 7330:GOTO 1940
2120 A=EPN:IF P(N(A))=0 AND J(N(A))=0 AND FJ(N(A))=0 AND W(N(A))=0 AND A<>CL THEN GOSUB 11280:COLOR 7,0:LOCATE B1+1,L2+2:GOSUB 11280:PRINT USING"Player # has no assets";N(A):GOSUB 10310:GOSUB 7330:GOTO 1940
2130 PL=VAL(A$):GOTO 11670
2150   REM 
2160   REM @@@@ JAIL PAYMENT OPTIONS @@@@
2170   REM 
2180 O$="BAIL-":O$(1)="$50":O$(2)="card":O=2
2190 OO(1)=1:IF FJ(N(PL))=0 THEN OO(2)=0 ELSE OO(2)=1
2200 IF J(N(PL))>3 THEN OO(0)=0 ELSE OO(0)=1
2210 GOSUB 9940:IF AA<>1 OR W(N(PL))=>50 THEN 2230
2220 CP((CL))=CP:AC1=PL:PL1=PL:NM=4:GOSUB 7170:DUE=50:GOSUB 8320:GOSUB 11650:GOSUB 11320:NM=0:IF J(N(PL1))<3 THEN IF W(N(PL))<50 THEN 2180 ELSE 2240 ELSE 2240
2230 GOSUB 7330:ON AA+1 GOTO 2300,2240,2250
2240 PA=-50:AC1=PL:PL1=PL:GOSUB 6590:J(N(PL))=0:GOTO 2270
2250 IF J1=N(PL) THEN J1=0 ELSE J2=0
2260 L=CL:GOSUB 1540:FJ(N(PL))=FJ(N(PL))-1:J(N(PL))=0
2270 COLOR PC(N(PL)),0:IF BN(CP(N(CL)))<3 THEN LOCATE Y(N(PL)),X(XN(N(PL)),N(PL)):PRINT " "
2280 Y(N(PL))=T1+4:IF CL=PL THEN Y=Y(N(PL))
2290 IF BN(CP(N(CL)))<3 THEN LOCATE Y(N(PL)),X(XN(N(L)),N(PL)):PRINT CHR$(L(N(PL)))
2300 L=PL:GOSUB 7240:IF NOROLL THEN NOROLL=0:GOTO 4570 ELSE 11650
2310   REM 
2320   REM  * DEAL *
2330 IF P(N(GV))=0 THEN 11680 ELSE GOSUB 10350
2340 DDN=0:DEAL=1:A=1:LASTP1=0:LASTA=1:SEE1=0:L=GV:Y1=T4:X1=L2+16:X1=L2:COLOR PC(N(GV)),0:POKE 1050,PEEK(1052)
2350 LOCATE B1+1,X1-1:PRINT"[         |     |          |     |  ]"
2360  REM  * opening variables *
2370 CE=E:CT=T:CPP=TP(N(GV)):V=BV(GV):FOR A1=1 TO NUM_OF_PLAYERS:H(A1)=BH(A1):CW(N(A1))=W(N(A1)):NEXT A1:CI$=""
2380 ERASE PD,CI:FOR A1=0 TO P(N(GV))-1:F=PP(N(GV),A1):CU(F)=U(F):CM(F)=M(F):CO(F)=O(F):NEXT A1
2390 GOSUB 2460:GOSUB 2400:GOTO 2620
2400 REM * display info *
2410 IF CO(P)<>N(GV) THEN COLOR PC(CO(P)),1 ELSE COLOR 14+CM(P),1
2420 LOCATE V,H(GV)+1:PRINT PROPERTY_SHORT_NAMES$((P);
2430 LOCATE V,H(GV)+5:IF CU(P)=0 THEN IF PD(P) THEN COLOR 14*(1+(PD(P)<0)),1:PRINT USING"####";PD(P); ELSE COLOR ,0:PRINT "    ";
2440 FOR L=1 TO NUM_OF_PLAYERS:IF CW(N(L))<>W(N(L)) THEN EXC=CW(N(L))-W(N(L)):GOSUB 1460 ELSE GOSUB 1510
2450 NEXT L:GOSUB 1900
2451 IF CO(P)<>N(GV) THEN COLOR PC(CO(P)),1 ELSE COLOR 14+CM(P),1
2452 LOCATE V,H(GV)+1:PRINT PROPERTY_SHORT_NAMES$((P);:RETURN
2460  REM  * sort *
2470 P=PP(N(GV),CPP)
2480 DD=1:DM=1:DH=1:DH1=1:DH2=1
2490 IF CO(P)<>N(GV) THEN DM=0:DH=0:DH1=0:DH2=0:GOTO 2590
2500 IF K(P)<3 THEN DH=0:DH1=0:DH2=0:GOTO 2580
2510 IF CU(P)=0 OR (CU(P)=5 AND CE>28) THEN DH2=0
2520 IF (CU(P)=>U(P) AND CW(N(GV))<50*BN(P)) OR CU(P)=5 OR (CU(P)<U(P) AND CW(N(GV))<25*BN(P)) OR (CU(P)=4 AND CT=>12) OR (CU(P)<4 AND CE=>32) THEN DH1=0
2530 FOR A1=1 TO 3:F=G(GN(P),A1):IF F=0 THEN 2570
2540 IF CM(F) OR CO(F)<>N(GV) THEN DH=0:DH1=0:DH2=0
2550 IF CU(F) THEN DM=0:DD=0
2560 DH1=DH1*ABS(1+(CU(F)<CU(P))):DH2=DH2*ABS(1+(CU(F)>CU(P)))
2570 NEXT A1:DH=DH1+DH2
2580 IF CM(P) THEN DM=DM*(1+(CW(N(GV))<C(P)/2+C(P)/20*M(P)))
2590 IF (CM(P) AND C(P)*.05>CW(N(RV))) OR NUM_OF_PLAYERS<2 THEN DD=0
2600 IF CO(P)<>N(GV) AND CW(N(GV))-CI(P)<0 THEN DD=0
2610 RETURN
2620 REM * inkey$ *
2630 A=LASTA
2640 IF (A=3 AND DH2=0) THEN IF DH1 THEN A=2:GOTO 2640 ELSE A=A+1 ELSE IF (A=1 AND DM=0) OR (A=2 AND DH1=0) OR (A=4 AND DD=0) THEN A=A+1 ELSE 2670
2650 IF A>6 THEN A=1 ELSE IF A<1 THEN A=6
2660 GOTO 2640
2670 GOSUB 3760
2680 A$=INKEY$:ON LEN(A$)+1 GOTO 2680,2810,2690
2690 A$=RIGHT$(A$,1):IF A$=CHR$(77) THEN AV=1:GOTO 2710
2700 IF A$<>CHR$(75) THEN 2730 ELSE AV=-1
2710 A=A+AV:IF A<1 THEN A=6 ELSE IF A>6 THEN A=1
2720 IF (A=1 AND DM=0) OR (A=2 AND DH1=0) OR (A=3 AND DH2=0) OR (A=4 AND DD=0) THEN 2710 ELSE LASTA=A:GOTO 2670 '2390
2730 IF A$=CHR$(80) THEN LCPP=CPP:GOTO 3640
2740 IF A$=CHR$(72) THEN LCPP=CPP:GOTO 3690
2750 IF A$<>CHR$(15) THEN 2800
2760 IF RV=1 THEN RV=NUM_OF_PLAYERS ELSE RV=RV-1
2770 IF DD THEN A=4
2780 IF RV=GV THEN 2760
2790 LASTA=A:GOSUB 3760:GOTO 2390
2800 IF A$=CHR$(83) THEN GOSUB 3370:GOTO 2390 ELSE 2680
2810 IF A$=CHR$(13) THEN IF A=6 THEN 2930 ELSE 2910
2820 IF A$="S" OR A$="s" THEN A=5:GOTO 2910
2830 IF (A$="T" OR A$="t") AND DD THEN A=4:GOTO 2910
2840 IF (A$="M" OR A$="m") AND DM THEN A=1:GOTO 2910
2850 IF A$="-" AND DH2 THEN A=3:GOTO 2910
2860 IF A$="+" AND DH1 THEN A=2:GOTO 2910
2870 IF A$<>CHR$(9) THEN 2900 ELSE IF RV=NUM_OF_PLAYERS THEN RV=1 ELSE RV=RV+1
2880 IF DD THEN A=4
2890 IF RV=GV THEN 2870 ELSE LASTA=A:GOSUB 3760:GOTO 2390
2900 IF (VAL(A$)>0 OR A$="0") AND DD THEN LASTA=A:GOSUB 3510:GOTO 2390 ELSE IF A$=CHR$(27) THEN 2930 ELSE 2680
2910 LASTA=A:ON A GOSUB 3340,3240,3280,3430,3220
2920 GOTO 2390
2930  REM  * end deal *
2940 REM color over last p_symbol
2950 GOSUB 7330:IF CO(P)<>N(GV) THEN COLOR PC(CO(P)),0 ELSE COLOR ZZ-CM(P),0
2960 LOCATE V,H(GV)+1:PRINT PROPERTY_SHORT_NAMES$((P);
2961 RTP(N(GV))=PP(N(GV),TP(N(GV)))
2962 IF A$=CHR$(13) THEN 3140
2970 REM erase money fields
2980 FOR L=1 TO NUM_OF_PLAYERS:GOSUB 1510:NEXT L
2990 REM redo gv's p_box
3010 L=GV:GOSUB 1600:GOSUB 1720
3020 REM if a property card was shown during this menu... erase it
3030 LASTP1=0:IF SEE1 THEN GOSUB 7020:SEE1=0 ELSE 3090
3040 REM if any kind of card was shown before this menu... put it back
3050 IF NM THEN SEE=1:P1=CP ELSE 3090
3060 IF PROPERTY_SHORT_NAMES$((P1)="?" THEN SC=1:GOSUB 7460:GOTO 3090
3070 IF PROPERTY_SHORT_NAMES$((P1)="$" THEN SC=2:GOSUB 7460:GOTO 3090
3080 GOSUB 5020:SEE=0
3090 REM zero out all variables used here
3100 REM erase pd,ci,co,cm,cu
3110 SEE=0:GOSUB 1860
3120 DEAL=0:DDN=0:IF A$=CHR$(13) AND NM=0 THEN 1940 ELSE IF A$=CHR$(27) THEN 11680 ELSE 11650
3130 REM
3140 REM * enter *
3150 ERASE PD:PPP=P:FOR A1=0 TO P(N(GV))-1:F=PP(N(GV),A1):M(F)=CM(F):U(F)=CU(F):O(F)=CO(F):NEXT A1:T=CT:E=CE:SB=BN(CP):GOSUB 1030
3160 REM redo p_box'S
3190 FOR L=1 TO NUM_OF_PLAYERS:W(N(L))=CW(N(L)):GOSUB 7240:GOSUB 1400:GOSUB 1510
3200 IF DDN OR L=GV THEN GOSUB 1600:GOSUB 1750
3210 NEXT L:GOSUB 12020:P=PPP:GOTO 3020
3220  REM  * see *
3230 P1=PP(N(GV),CPP):IF P1=LASTP1 THEN LASTP1=0:GOSUB 7020:RETURN ELSE SEE1=SEE1+1:COLOR ZZ,0:LOCATE T2,L2+1/2*(R2-L2)-3:PRINT"ÍÍÍÍÍÍ":SEE=1:LASTP1=P1:GOSUB 5020:SEE=0:P1=0:RETURN
3240  REM  * add houses *
3250 CU(P)=CU(P)+1:IF CU(P)=5 THEN CT=CT+1:CE=CE-4 ELSE CE=CE+1
3260 IF CU(P)>U(P) THEN WV=-50*BN(P) ELSE WV=-25*BN(P)
3270 GOTO 3310
3280 REM  * subtract houses *
3290 CU(P)=CU(P)-1:IF CU(P)=4 THEN CT=CT-1:CE=CE+4 ELSE CE=CE-1
3300 IF CU(P)=>U(P) THEN WV=50*BN(P) ELSE WV=25*BN(P)
3310 PD(P)=PD(P)+WV:CW(N(GV))=CW(N(GV))+WV:COLOR 2,0:LOCATE V,H(GV)+5
3320 BP=P:GOSUB 11050
3330 RETURN
3340  REM  * mortgage *
3350 WV=C(P)*(.5+.05*M(P)):IF CM(P) THEN CM(P)=0:WV=-WV ELSE CM(P)=1
3360 PD(P)=PD(P)+WV:CW(N(GV))=CW(N(GV))+WV:RETURN
3370  REM  * delete *
3380 IF CO(P)<>O(P) AND DD THEN GOSUB 3420:GOSUB 2460
3390 IF CM(P)<>M(P) AND DM THEN GOSUB 3340:GOSUB 2460
3400 IF CU(P)<U(P) AND DH1 THEN GOSUB 3240:GOSUB 2460:GOTO 3400
3410 IF CU(P)>U(P) AND DH2 THEN GOSUB 3280:GOSUB 2460:GOTO 3410 ELSE RETURN
3420  REM  * trade *
3430 RRV=RV:PWV=CI(P)
3440 IF CO(P)=N(GV) THEN CO(P)=N(RV):GOTO 3490
3450 RV=UN(CO(P)):CO(P)=N(GV):CW(N(GV))=CW(N(GV))-CI(P):PD(P)=PD(P)-CI(P):CW(N(RV))=CW(N(RV))+CI(P):CI(P)=0
3460 IF PM(P)=1 THEN WV=C(P)*.55:PM(P)=0:CM(P)=1 ELSE IF PM(P)=2 THEN WV=.05*C(P):PM(P)=0 ELSE 3480
3470 CW(N(RV))=CW(N(RV))+WV:GOTO 3480
3480 DDN=DDN-1:GOTO 3540
3490 IF CM(P) THEN GOSUB 3570:X1=L2+16:Y1=B1+1:CC=PC(N(PL)):X1=L2:COLOR PC(N(GV)),0:LOCATE B1+1,X1-1:PRINT"[         |     |          |     |  ]":IF PM(P) THEN 3500 ELSE CO(P)=N(GV):GOTO 3560 ELSE 3500
3500 DDN=DDN+1:GOTO 3540
3510 RRV=RV:IF CM(P)<>0 AND PM(P)=0 THEN GOSUB 3570 ELSE 3530
3520 X1=L2+16:Y1=B1+1:X1=L2:COLOR PC(N(GV)),0:LOCATE B1+1,X1-1:PRINT"[         |     |          |     |  ]":IF PM=0 THEN 3560
3530 IF CW(N(RV))-VAL(RIGHT$(STR$(CI(P)),3)+A$)+VAL(CI$)<0 THEN 3560 ELSE IF VAL(CI$)=0 AND CO(P)=N(GV) THEN DDN=DDN+1
3531 CO(P)=N(RV)
3540 CI$=STR$(CI(P)):LC=VAL(CI$):LCPP=CPP:CI$=RIGHT$(CI$,3)+A$:CI(P)=VAL(CI$)
3550 WV=+VAL(CI$)-LC:CW(N(RV))=CW(N(RV))-WV:CW(N(GV))=CW(N(GV))+WV:PD(P)=PD(P)+WV
3560 RV=RRV:RETURN
3570  REM  * Unmortgage?-now or later *
3580 GOSUB 7330:IF CW(N(RV))<.55*C(P) THEN OO(2)=0 ELSE OO(2)=1
3590 O$="Unmortgage-":O$(1)="later":O$(2)="now":OO(1)=1:OO(0)=1:O=2
3600 GOSUB 9940:IF AA=1 THEN PM(P)=2:WV=-.05*C(P) ELSE IF AA=2 THEN PM(P)=1:WV=-.55*C(P):CM(P)=0 ELSE 3620
3610 CW(N(RV))=CW(N(RV))+WV
3620 GOSUB 7330:RETURN
3630  REM  * move *
3640 LC=CI(P)
3650 LV=V:IF CPP=P(N(GV))-1 THEN IF P(N(GV))<=W# THEN V=BV(GV):CPP=0:GOTO 3730 ELSE CPP=0 ELSE CPP=CPP+1
3660 IF V<BV(GV)+W#-1 THEN V=V+1:GOTO 3730 ELSE TP(N(GV))=TP(N(GV))+1:IF TP(N(GV))>P(N(GV))-1 THEN TP(N(GV))=0
3670 L=GV:GOSUB 1750:V=V-1:GOTO 3750
3680 '
3690 LC=CI(P)
3700 LV=V:IF CPP=0 THEN IF P(N(GV))<=W# THEN CPP=P(N(GV))-1:V=BV(GV)+P(N(GV))-1:GOTO 3730 ELSE CPP=P(N(GV))-1 ELSE CPP=CPP-1
3710 IF V>BV(GV) THEN V=V-1:GOTO 3730 ELSE IF TP(N(GV))=0 THEN TP(N(GV))=P(N(GV))-1 ELSE TP(N(GV))=TP(N(GV))-1
3720 L=GV:GOSUB 1750:V=BV(GV):GOTO 3750
3730 IF CO(PP(N(GV),LCPP))<>N(GV) THEN COLOR PC(CO(PP(N(GV),LCPP))),0 ELSE COLOR ZZ-CM(PP(N(GV),LCPP)),0
3740 LOCATE LV,BH(GV)+1:PRINT PROPERTY_SHORT_NAMES$((PP(N(GV),LCPP));
3750 P=PP(N(GV),CPP):CI$=STR$(CI(P)):GOSUB 2460:GOSUB 2451:GOTO 2620
3760 REM         * menu *
3770 PC=PC(N(GV)):LOCATE B1+1,X1:IF A=1 THEN COLOR 14,1:PRINT "MORTGAGE";:GOTO 3790 ELSE IF DM THEN COLOR PC,0 ELSE COLOR ZZ,0
3780 PRINT "mortgage";
3790 IF DH THEN COLOR PC,0 ELSE COLOR ZZ,0
3800 LOCATE B1+1,X1+12:PRINT "";
3810 IF A=2 THEN COLOR 14,1 ELSE IF DH1 THEN COLOR PC,0 ELSE COLOR ZZ,0
3820 LOCATE B1+1,X1+11:PRINT "+";
3830 IF A=3 THEN COLOR 14,1 ELSE IF DH2 THEN COLOR PC,0 ELSE COLOR ZZ,0
3840 LOCATE B1+1,X1+13:PRINT "-";
3850 LOCATE B1+1,X1+17:IF A=4 THEN COLOR 14,1:PRINT USING"TRADE(#)";RV;:GOTO 3870 ELSE IF DD THEN COLOR PC,0 ELSE COLOR ZZ,0
3860 PRINT USING "trade(#)";RV;
3870 LOCATE B1+1,X1+28:IF A=5 THEN COLOR 14,1:PRINT "SEE"; ELSE COLOR PC,0:PRINT "see";
3880 IF A=6 THEN COLOR 14,1 ELSE COLOR PC,0
3890 LOCATE B1+1,X1+34:PRINT "û";
3900 RETURN
3910  REM 
3920  REM @@@@ SEE @@@@
3930  REM 
3940 AA=1:OAA=AA
3950 O$(1)="°?°":O$(2)="?":O$(3)="$":O$(4)="go":O$(5)="jail":O$(6)="free":O$(7)="go2":O=7:AA=OAA:GOSUB 10010:OAA=AA
3960 IF AA>3 THEN 3980 ELSE ON AA+1 GOTO 3970,4010,3990,3990
3970 SB=BN(CP(N(CL))):GOSUB 1020:GOTO 10410
3980 SB=AA-3:GOSUB 1020:GOTO 3950
3990 SC=AA-1:GOTO 7350
4000  REM 
4010  REM @@@@ SEE A PROPERTY CARD @@@@
4020  REM 
4030 POKE 1050,PEEK(1052):P=1:X1=L2+16
4040 LASTP1=0:L=PL:X1=L2
4050 LOCATE B1+1,X1+1
4060 IF K(P)>2 THEN 4090 ELSE COLOR 7,0:IF P<>28 AND P<>12 THEN 4080
4070 PRINT"ÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛÛ":GOTO 4100
4080 PRINT"XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX":GOTO 4100
4090 COLOR FC(P),BC(P):PRINT"±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±"
4100 COLOR ZZ,0
4110 SP=1/2*((R2-L2)-LEN(PROPERTY_NAMES$(P)))
4120 LOCATE B1+1,X1+SP-1
4130 PRINT " "PROPERTY_NAMES$(P)" "
4140 A$=INKEY$
4150 IF A$=CHR$(13) THEN IF P=LASTP1 THEN LASTP1=0:GOSUB 7020:GOTO 4140 ELSE 4290
4160 IF A$=CHR$(27) THEN LASTP1=0:GOSUB 7330:GOSUB 7030:GOTO 3950
4170 IF LEN(A$)=2 AND RIGHT$(A$,1)=CHR$(75) THEN V1=-1:N=10:GOTO 4210
4180 IF LEN(A$)=2 AND RIGHT$(A$,1)=CHR$(77) THEN V1=1:N=10:GOTO 4210
4190 IF LEN(A$)=2 AND RIGHT$(A$,1)=CHR$(72) THEN V1=-1:N=1:GOTO 4210
4200 IF LEN(A$)=2 AND RIGHT$(A$,1)=CHR$(80) THEN V1=1:N=1:GOTO 4210 ELSE 4140
4210 P=P+(V1*N)
4220 IF P>39 THEN P=P-40
4230 IF P<1 THEN P=P+40
4240 IF K(P)<2 THEN P=P+V1:GOTO 4220
4250 GOTO 4050
4260 GOSUB 7060
4270 IF NM=0 THEN 3910
4280 SEE=1:P1=CP:GOSUB 5020:GOTO 3910
4290 LASTP1=P:P1=P:SEE=1:LOCATE T2,L2+1/2*(R2-L2)-3:PRINT"ÍÍÍÍÍÍ":GOSUB 5020:GOTO 4140
4300   REM 
4310   REM @@@@ ROLL DICE @@@@
4320   REM 
4330 RANDOMIZE TIMER:D1=INT(RND*6+1):RANDOMIZE TIMER:D2=INT(RND*6+1)
4340 IP=1:GOSUB 11180:IF IP=1 THEN D1=INK:GOSUB 11200:D2=INK
4350 DR=D1+D2
4360  REM 
4370  REM @@@@ SHOW DIEROLL @@@@
4380  REM 
4390 FOR D=1 TO D1:IF SOUNDS THEN PLAY "mf O4 A64 P64"
4400 LOCATE B1+1,L2+9:COLOR PC(N(CL)),0
4410 PRINT USING"[#]";D:NEXT D:IF SOUNDS THEN PLAY"P16"
4420 FOR D=1 TO D2:IF SOUNDS THEN PLAY "O4 A64 P64"
4430 LOCATE B1+1,L2+21:COLOR PC(N(CL)),0:PRINT USING"[#]";D:NEXT D
4440  REM 
4450  REM @@@@ DOUBLES @@@@
4460  REM 
4470 IF D1<>D2 THEN 4540 ELSE IF DB<2 THEN PLY$="A" ELSE PLY$="<A"
4480 LOCATE B1+1,L2+13:COLOR 30,1:PRINT "Doubles"
4490 IF SOUNDS THEN PLAY"Mf O3 A64 P64 A64 P64 A64 P32 X"+VARPTR$(PLY$)
4500 GOSUB 10310:LOCATE B1+1,L2+13:COLOR ,0:PRINT "       "
4510 IF J(N(CL)) THEN LOCATE Y,X:COLOR ,0:PRINT " ":J(N(CL))=0:Y=T1+4:Y(N(CL))=Y:DB=0:GOTO 4590
4520 DB=DB+1:IF DB<3 THEN GOSUB 11530:GOTO 4590 ELSE LOCATE Y,X:COLOR ,0:PRINT " ":DB=0:J(N(CL))=1
4530 CP=10:CP(N(CL))=CP:Y=T1+1:Y(N(CL))=Y:NX=R1-KK+CL:X(1,N(CL))=NX:L=CL:GOSUB 7330:GOSUB 7240:CA=CL:GOSUB 8510:GOTO 7070
4540 DB=0:GOSUB 11530:IF J(N(CL))=0 THEN 4590 ELSE J(N(CL))=J(N(CL))+1:COLOR 30,1:LOCATE B1+1,L2+14
4550 PRINT USING "Day #";J(N(CL))-1:GOSUB 10310:GOSUB 7330:IF J(N(CL))<4 THEN  7150 ELSE COLOR ,0
4560 LOCATE B1+1,L2+9:PRINT"               ":NOROLL=1:GOTO 2150
4570 LOCATE Y(N(CL)),X(1,N(CL)):PRINT " "
4580 LOCATE B1+1,L2+9:PRINT USING"[#]         [#]";D1;D2:J(N(CL))=0:Y=T1+4
4590   REM 
4600   REM @@@@ CALCULATE PROPERTY TO LAND ON @@@@
4610   REM 
4620 LP=CP:FOR A=1 TO DR:CP=CP+1-2*BW:IF CP<0 THEN CP=40 ELSE IF CP>39 THEN CP=0
4630 NEXT A
4640   REM 
4650   REM @@@@ MOVE PLAYER @@@@
4660   REM 
4670 NX=X:X=(B(BN(CP),10)-CP-1)*6+13+L1:IF K(CP)=0 THEN X=L1+KK
4680 IF CB THEN Y=T1+4:CB=0
4690 LX=NX:IF J(N(CL))=0 THEN NY=T1+4
4700 NX=NX-1+2*BW
4710 IF BW=PO AND NX=L1+KK THEN PA=200:PL1=PL:AC1=PL:GOSUB 6590
4720 IF NX=X AND PO=CP THEN AR=1
4730 IF SCREEN(Y,NX)=179 THEN PO=PO+1-2*BW ELSE 4760
4740 IF BW THEN IF PO<=0 THEN PO=40 ELSE 4750 ELSE IF PO=>40 THEN PO=0
4750 IF MVE THEN MVE=0:AR=1:IF LP=CP THEN NX=LX:GOTO 4770 ELSE NX=X:NY=T1+2:GOTO 4770 ELSE 4700
4760 IF SCREEN(NY,NX)=32 THEN 4770 ELSE IF PO=CP THEN MVE=1:GOTO 4700 ELSE 4700
4770 LOCATE Y,LX:COLOR PC(N(CL)),0:PRINT" ";:LOCATE NY,NX:PRINT CHR$(L(N(CL)))
4780 Y=NY:IF AR THEN AR=0:BW=0:MVE=0:GOTO 4820 ELSE LP=PO
4790 IF (BW OR NX>L1+KK) AND (BW=0 OR NX<R1-KK) THEN 4810
4800 CB=1:LOCATE Y,NX:COLOR ,0:PRINT " ";:IF BW THEN NX=L1+KK:GOTO 960 ELSE NX=R1-2:GOTO 960
4810 GOTO 4680
4820 IF K(CP)>0 THEN 4850
4830 Y(N(CL))=Y:IF NX<L1+10 THEN COLOR PC(N(CL)),0:LOCATE Y(N(CL)),X(2,N(CL)):PRINT CHR$(L(N(CL))):LOCATE Y(N(CL)),NX:PRINT" "
4840 NX=R1-KK+N(CL)
4850 COLOR ,0:LOCATE B1+1,L2:PRINT"                                   ":CP(N(CL))=CP:L=CL:GOSUB 7240:P1=CP:X=NX:X(1,N(CL))=NX
4860 REM
4870 REM @@@@ DEFINE CURRENT PROPERTY @@@@
4880 REM
4890 REM `PLAYY' IS CODED AS FOLLOWS ...
4900 REM  1=CHANCE CARD                 6=ANY OTHER CORNER PROPERTY
4910 REM  2=COMMUNITY CHEST CARD        7=OWN PLAYER'S PROPERTY
4920 REM  3=LUXURY TAX                  8=NO OWNER...MAY BUY PROPERTY
4930 REM  4=INCOME TAX                  9=NO MORTGAGE & OWNED..MUST PAY RENT
4940 REM  5=GO TO JAIL                 10=PROPERTY MORTGAGED... NO RENT
4950 REM       11=`PAYTWICE' ADVANCE FROM CHANCE
4960 IF PROPERTY_SHORT_NAMES$((P1)="?" THEN SC=1:PLAYY=1:GOSUB 7460:GOTO 7620
4970 IF PROPERTY_SHORT_NAMES$((P1)="$" THEN SC=2:PLAYY=2:GOSUB 7460:GOTO 7620
4980 IF PROPERTY_NAMES$(P1)="LUXURY TAX" THEN PLAYY=3:GOTO 6830
4990 IF PROPERTY_NAMES$(P1)="INCOME TAX" THEN PLAYY=4:GOTO 6760
5000 IF PROPERTY_NAMES$(P1)="GO TO JAIL" THEN PLAYY=5:GOTO 6870
5010 IF K(P1)<2 THEN GO2=0:PLAYY=6:GOTO 7070 ELSE 5800
5020  REM 
5030  REM @@@@ SHOW PROPERTY INFORMATION @@@@
5040  REM 
5050 IF RL<>0 THEN ON K(P1)-1 GOTO 5320,5090
5060 COLOR ZZ,0:LOCATE T2,L2+10:PRINT STRING$(15,CHR$(205))
5070 SP=1/2*((R2-L2)-LEN(PROPERTY_NAMES$(P1)))
5080 IF K(P1)=2 THEN 5320
5090 IF RL>0 THEN COLOR 30,1 ELSE COLOR ZZ,0
5100 IF RL THEN ON ABS(RL) GOTO 5110,5200,5210,5220,5230,5240,5250
5110 LOCATE T2,L2+1/2*(R2-L2)-3
5120 IF C(P1)>99 THEN PRINT USING"[$###.]";C(P1) ELSE PRINT USING"[$##.]";C(P1)
5130 IF RL THEN  5310 ELSE BC=BC(P1):FC=FC(P1)
5140 IF K(P1)=2 THEN 5320 ELSE IF RL THEN 5090 ELSE BC=BC(P1):FC=FC(P1)
5150 COLOR FC,BC
5160 LOCATE T2+1,L2+1:PRINT"±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±"
5170 LOCATE ,L2+1:PRINT"±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±"
5180 LOCATE ,L2+1:PRINT"±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±"
5190 COLOR ZZ,0:SP=1/2*((R2-L2)-LEN(PROPERTY_NAMES$(P1))):LOCATE T2+2,L2+SP-1:PRINT " "PROPERTY_NAMES$(P1)" ":LOCATE T2+4,L2+1:PRINT "                                "
5200 LOCATE T2+5,L2+1:PRINT USING"            Rent  $$##.         ";RENT_RATES(0,P1):IF RL THEN  5310
5210 LOCATE T2+6,L2+1:PRINT USING" With 1 House              $###.";RENT_RATES(1,P1):IF RL THEN  5310
5220 LOCATE T2+7,L2+1:PRINT USING" With 2 Houses              ###.";RENT_RATES(2,P1):IF RL THEN  5310
5230 LOCATE T2+8,L2+1:PRINT USING" With 3 Houses             ####.";RENT_RATES(3,P1):IF RL THEN  5310
5240 LOCATE T2+9,L2+1:PRINT USING" With 4 Houses             ####.";RENT_RATES(4,P1):IF RL THEN  5310
5250 LOCATE T2+10,L2+1:PRINT USING"      With HOTEL $$####.        ";RENT_RATES(5,P1):IF RL THEN  5310
5260 LOCATE T2+11,L2+1:PRINT"                               "
5270 LOCATE T2+12,L2+1:PRINT USING"     Mortgage Value $$###.      ";1/2*C(P1)
5280 LOCATE T2+13,L2+1:PRINT USING"     Houses cost $$###. each    ";50*BN(P1)
5290 LOCATE T2+14,L2+1:PRINT USING"   Hotels, $$###. plus 4 houses ";50*BN(P1)
5300 LOCATE T2+15,L2+1:PRINT"                                ";
5310 IF RL>0 THEN RL=-RL:GOSUB 10310:GOTO 5090 ELSE RL=0:RETURN
5320  REM 
5330  REM @@@@ DRAW A R/R CARD @@@@
5340  REM 
5350 IF B(BN(P1),5)<>P1 THEN 5590
5360 IF RL>0 THEN COLOR 30,1 ELSE COLOR ZZ,0
5370 IF RL THEN ON ABS(RL) GOTO 5380,5470,5480,5490,5500
5380 LOCATE T2,L2+1/2*(R2-L2)-3:PRINT USING "[$###.]";C(P1):IF RL THEN 5550
5390 COLOR ZZ,0:SP=1/2*((R2-L2)-LEN(PROPERTY_NAMES$(P1)))
5400 LOCATE T2+1,L2+1:PRINT "                                "
5410 LOCATE T2+2,L2+1:PRINT "              ÜÛ               "
5420 LOCATE T2+3,L2+1:PRINT "              ø ø               "
5430 LOCATE T2+4,L2+1:PRINT " ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ "
5440 LOCATE T2+5,L2+1:PRINT SPACE$(SP-1)PROPERTY_NAMES$(P1)SPACE$(R2-(L2+SP+LEN(PROPERTY_NAMES$(P1)))-1)
5450 LOCATE T2+6,L2+1:PRINT " ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ "
5460 LOCATE T2+7,L2+1:PRINT "                                "
5470 LOCATE T2+8,L2+1:PRINT " Rent                      $25. ":IF RL THEN 5550
5480 LOCATE T2+9,L2+1:PRINT " If 2 R.R's are owned       50. ":IF RL THEN 5550
5490 LOCATE T2+10,L2+1:PRINT " If 3  "CHR$(34)"     "CHR$(34)"   "CHR$(34)"         100. ":IF RL THEN 5550
5500 LOCATE T2+11,L2+1:PRINT " If 4  "CHR$(34)"     "CHR$(34)"   "CHR$(34)"         200. ":IF RL THEN 5550
5510 LOCATE T2+12,L2+1:PRINT "                                "
5520 LOCATE T2+13,L2+1:PRINT "                                "
5530 LOCATE T2+14,L2+1:PRINT " Mortgage Value           $100. "
5540 LOCATE T2+15,L2+1:PRINT "                                ";
5550 IF RL>0 THEN RL=-RL:GOSUB 10310:GOTO 5360 ELSE RL=0:RETURN
5560 REM
5570 REM @@@@ DRAW A UTILITY CARD @@@@
5580 REM
5590 IF P1<>12 AND P1<>28 THEN RETURN
5600 IF RL>0 THEN COLOR 30,1 ELSE COLOR ZZ,0
5610 IF RL THEN ON ABS(RL) GOTO 5620,5710,5740
5620 LOCATE T2,L2+1/2*(R2-L2)-3:PRINT USING "[$###.]";C(P1):IF RL THEN 5790
5630 COLOR FC(P1),0
5640 LOCATE T2+1,L2+1:PRINT SPACE$(R2-L2-1):LOCATE T2+2,L2+1:PRINT "              ";PROPERTY_SHORT_NAMES$((P1);SPACE$(15);
5650 COLOR ZZ,0
5660 LOCATE T2+3,L2+1:PRINT "                                "
5670 LOCATE T2+4,L2+1:PRINT " ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ "
5680 LOCATE T2+5,L2+1:PRINT SPACE$(SP-1)PROPERTY_NAMES$(P1)SPACE$(R2-(L2+SP+LEN(PROPERTY_NAMES$(P1)))-1)
5690 LOCATE T2+6,L2+1:PRINT " ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ "
5700 LOCATE T2+7,L2+1:PRINT "                                "
5710 LOCATE T2+8,L2+1:PRINT"   If  one  "CHR$(34)"Utility"CHR$(34)"  is  owned"
5720 LOCATE T2+9,L2+1:PRINT"rent is 4 times the amount shown"
5730 LOCATE T2+10,L2+1:PRINT"on dice.                        ":IF RL  THEN 5790
5740 LOCATE T2+11,L2+1:PRINT"  If   both   "CHR$(34)"Utilities"CHR$(34)"   are"
5750 LOCATE T2+12,L2+1:PRINT" owned  rent  is    10    times"
5760 LOCATE T2+13,L2+1:PRINT" amount   shown    on     dice.":IF RL THEN 5790
5770 LOCATE T2+14,L2+1:PRINT"                                "
5780 LOCATE T2+15,L2+1:PRINT"Mortgage Value              $75.";
5790 IF RL>0 THEN RL=-RL:GOSUB 10310:GOTO 5600 ELSE RL=0:RETURN
5800  REM 
5810  REM @@@@ CHECK PROPERTY OWNERSHIP @@@@
5820  REM 
5830 L=CL:GOSUB 7240:GO2=0:IF O(CP)=N(CL) THEN PT=0:GOSUB 11570:PLAYY=7:GOSUB 5870:GOTO 7070
5840 IF O(CP)=0 THEN PT=0:GOSUB 11570:PLAYY=8:GOSUB 5020:RL=1:P1=CP:GOSUB 5020:GOTO 6000
5850 IF M(CP)=0 THEN PLAYY=9-2*(PLAYY=1):GOSUB 5870:P1=CP:GOSUB 5020:GOTO 6380
5860 PT=0:GOSUB 11570:PLAYY=10:GOSUB 5870:GOTO 7070
5870  REM
5880  REM * BLINK PROPERTY *
5890  REM
5900 H(UN(O(CP)))=BH(UN(O(CP)))
5910 V=BV(UN(O(CP)))+PPN(CP)-1-TP(O(CP))+P(O(CP))*(1+(PPN(CP)=>TP(O(CP))))
5920 IF V=>BV(UN(O(CP))) AND V<=BV(UN(O(CP)))+W#-1 THEN 5970
5930 IF O(CP)=O(G(GN(CP),1)) THEN TP(O(CP))=PPN(G(GN(CP),1))-1:RTP(O(CP))=G(GN(CP),1):GOTO 5960
5940 IF O(CP)=O(G(GN(CP),2)) THEN TP(O(CP))=PPN(G(GN(CP),2))-1:RTP(O(CP))=G(GN(CP),2):GOTO 5960
5950 IF O(CP)=O(G(GN(CP),3)) THEN TP(O(CP))=PPN(G(GN(CP),3))-1:RTP(O(CP))=G(GN(CP),3) ELSE TP(O(CP))=PPN(CP)-1:RTP(O(CP))=PP(O(CP),PPN(CP)-1)
5960 L=O(CP):GOSUB 1740:GOTO 5910
5970 LOCATE V,H(UN(O(CP)))+1:COLOR 30+M(CP),1:PRINT PROPERTY_SHORT_NAMES$((CP);
5980 GOSUB 10310:LOCATE V,H(UN(O(CP)))+1:COLOR ZZ-M(CP),0:PRINT PROPERTY_SHORT_NAMES$((CP);
5990 RETURN
6000  REM 
6010  REM @@@@ LET PLAYER BUY PROPERTY @@@@
6020  REM 
6030 CC=PC(N(CL)):L=CL:O$(1)="buy":O$(2)="auction":O=2:OO(0)=0:OO(1)=1:OO(2)=1:GOSUB 9940
6040 IF AA<>1 OR W(N(CL))=>C(CP) THEN 6060
6050 CP(N(CL))=CP:PL1=CL:PL=CL:NM=1:GOSUB 7170:DUE=C(CP):GOSUB 8320:GOSUB 11650:GOSUB 11320:NM=0:IF W(N(CL))<C(CP) THEN 6030 ELSE AA=1
6060 ON AA GOTO 6070,6090
6070 PA=-C(CP):AC1=CL:PL1=CL:GOSUB 6590
6080 O(CP)=N(CL):L=CL:GOSUB 1600:GOSUB 1710:GOTO 7010
6090  REM 
6100  REM @@@@ AUCTION OFF PROPERTY @@@@
6110  REM 
6120 POKE 1050,PEEK(1052)
6130 X1=L2+16:Y1=B1+1:CIV=1:L=CL:X1=L2:FOR L=1 TO NUM_OF_PLAYERS:I(N(L))=0:NEXT L:HB=CL:IF CL=NUM_OF_PLAYERS THEN CI=1 ELSE CI=CL+1
6140 CI$="0"
6150 GOSUB 11320
6160 LOCATE B1,L2+6:COLOR PC(N(HB)),0:PRINT USING "[High bid **$#####,]";I(N(HB))
6170 I$=INKEY$:IF I$<>CHR$(13) THEN 6240
6180 L=CI:GOSUB 1510:CI$=RIGHT$(CI$,6)
6190 IF VAL(CI$)=0 THEN 6140
6200 I(N(CI))=VAL(CI$):IF I(N(CI))>0 THEN FOR L=1 TO NUM_OF_PLAYERS ELSE 6140
6210 IF I(N(CI))<=I(N(L)) AND L<>CI THEN CHECK=1
6220 NEXT L:IF CHECK THEN CHECK=0:GOTO 6170 ELSE HB=CI:CI=CI+CIV:IF CI>NUM_OF_PLAYERS THEN CI=1 ELSE IF CI<1 THEN CI=NUM_OF_PLAYERS
6230 GOTO 6140
6240 IF LEN(I$)=2 AND RIGHT$(I$,1)=CHR$(83) THEN CI$="0"
6250 IF I$<>CHR$(9) AND I$<>CHR$(27) THEN 6280
6260 GOSUB 11320
6270 FOR L=1 TO NUM_OF_PLAYERS:GOSUB 1510:NEXT L:GOSUB 7330:IF I$=CHR$(27) THEN 6000 ELSE 6360
6280 IF LEN(I$)=2 AND RIGHT$(I$,1)=CHR$(77) THEN CIV=1:GOTO 6300
6290 IF LEN(I$)=2 AND RIGHT$(I$,1)=CHR$(75) THEN CIV=-1 ELSE 6320
6300 L=CI:GOSUB 1510:CI=CI+CIV
6310 IF CI>NUM_OF_PLAYERS THEN CI=1 ELSE IF CI<1 THEN CI=NUM_OF_PLAYERS
6320 IF VAL(I$) OR I$="0" THEN 6330 ELSE 6340
6330 CI$=RIGHT$(CI$,3)+I$
6340 CI$=RIGHT$(CI$,6)
6350 EXC=-VAL(CI$):L=CI:GOSUB 1460:GOTO 6170
6360 GOSUB 7330:CI=HB:PA=-I(N(CI)):AC1=CI:PL1=CI:GOSUB 6590:O(CP)=N(CI):L=CI:CI=0
6370 GOSUB 1390:GOSUB 1600:GOSUB 1710:GOTO 7010
6380  REM 
6390  REM @@@@ PLAYER PAYS RENT TO OWNER @@@@
6400  REM 
6410 RR=1:FOR P=1 TO 4:IF G(GN(CP),P)<>CP AND O(G(GN(CP),P))=O(CP) THEN RR=RR+1
6420 NEXT P:ON K(CP)-1 GOTO 6430,6530
6430 ON GN(CP)-8 GOTO 6450,6470
6440 REM @@@@ RENT ON R/R @@@@
6450 RL=RR+1:P1=CP:GOSUB 5020:R=25*2^(RR-1):GOTO 6570
6460 REM @@@@ RENT ON UTLITIES @@@@
6470 ON RR GOTO 6480,6490
6480 R=4*DR:RL=2:GOTO 6500
6490 R=10*DR:RL=3
6500 P1=CP:GOSUB 5020:IF PT THEN R=10*DR
6510 GOTO 6580
6520 REM @@@@ RENT ON PROPERTY CARD @@@@
6530 R=RENT_RATES(U(CP),CP):RL=U(CP)+2:P1=CP:GOSUB 5020
6540 IF GN(CP)=1 OR GN(CP)=8 THEN 6550 ELSE 6560
6550 IF RR=2 AND U(CP)=0 THEN R=2*R:GOTO 6570
6560 IF RR=3 AND U(CP)=0 THEN R=2*R
6570 IF PT THEN R=R*2
6580 AC1=CL:AC2=UN(O(CP)):PA=-R:PB=R:PL1=PL:GOSUB 6590:PT=0:GOSUB 11570:GOTO 7010
6590  REM 
6600  REM @@@@ COLLECT/PAY MONEY @@@@
6610  REM 
6620 IF CD THEN AC1=CL
6630 IF PA=0 THEN IF PB=0 THEN 6730 ELSE 6700
6640 IF ALL THEN AL=1:FOR AC2=AC2 TO NUM_OF_PLAYERS:IF AC2=AC1 THEN 6740
6650 IF W(N(AC1))+PA=>0 THEN 6670
6660 L=AC1:DUE=ABS(PA):GOSUB 8320:CP(N(CL))=CP:NM=3:PL=AC1:PL1=AC1:PL2=AC2:PLA=PA:PLB=PB:GOSUB 7170:GOSUB 12123:GOSUB 11650:GOSUB 11320:NM=0:AC1=PL1:AC2=PL2:PA=PLA:PB=PLB:GOTO 6600
6670 COLOR 14,1:W(N(AC1))=W(N(AC1))+PA:L=AC1:EXC=PA:GOSUB 1460
6680 IF PB=0 THEN GOSUB 10310:GOTO 6730
6690 IF W(N(AC2))+PB=>0 THEN 6710
6700 L=AC2:DUE=ABS(PB):GOSUB 8320:CP(N(CL))=CP:NM=5:PL=AC2:PL1=AC2:PL2=AC1:PLA=PA:PLB=PB:GOSUB 7170:GOSUB 12123:GOSUB 11650:GOSUB 11320:NM=0:AC1=PL2:AC2=PL1:PA=PLA:PB=PLB:GOTO 6590
6710 W(N(AC2))=W(N(AC2))+PB:L=AC2:EXC=PB:GOSUB 1460:GOSUB 10310
6720 L=AC2:GOSUB 1510:GOSUB 1400
6730 L=AC1:GOSUB 1510:GOSUB 1400
6740 IF AL THEN NEXT AC2:AL=0
6750 PA=0:PB=0:AC1=0:AC2=0:ALL=0:RETURN
6760  REM 
6770  REM @@@@ INCOME TAX @@@@
6780  REM 
6790 O$="Income Tax Payment-":O$(1)="$200":O$(2)="10%":O=2:OO(0)=0:OO(1)=1:OO(2)=1:GOSUB 9940
6800 ON AA GOTO 6820,6810
6810 PA=-W(N(CL)):COLOR 14,1:FOR A=0 TO P(N(CL))-1:F=PP(N(CL),A):PA=PA-C(F)-50*BN(F)*U(F):L=CL:EXC=PA*.1:GOSUB 1460:NEXT A:PA=PA*.1:AC1=PL:PL1=PL:GOSUB 6590:GOTO 7010
6820 PA=-200:AC1=PL:PL1=PL:GOSUB 6590:GOTO 7010
6830  REM 
6840  REM @@@@ LUXURY TAX @@@@
6850  REM 
6860 PA=-75:AC1=PL:PL1=PL:GOSUB 6590:GOTO 7010
6870  REM 
6880  REM @@@@ GO TO JAIL @@@@
6890  REM 
6900 DB=0:GOSUB 11530:COLOR PC(N(CL)),0:IF CD THEN LOCATE Y,NX ELSE LOCATE Y,X(2,N(CL))
6910 PRINT " ":CP=10:CP(N(CL))=CP:Y=T1+1:Y(N(CL))=Y:NX=R1-KK+CL:X(1,N(CL))=NX:J(N(CL))=1:L=CL:GOSUB 7240:CA=CL:GOSUB 8510:GOTO 7010
6920  REM 
6930  REM @@@@ ADVANCE TO GO @@@@
6940  REM 
6950 PA=200:AC1=CL:PL1=CL:GOSUB 6590
6960 COLOR PC(N(CL)),0:LOCATE Y,NX:PRINT " ":SB=BN(CP):G2=CL:GOSUB 1020:G2=0
6970 X=(B(BN(CP),10)-CP-1)*6+13+L1:NXV=1:NX=X:Y=T1+4
6980 IF SCREEN(Y,NX)<>32 THEN IF SCREEN(Y,NX)=179 THEN IF NXV=1 THEN NX=X:NXV=-1 ELSE Y=T1+2:NX=X:GOTO 7000 ELSE 6990 ELSE 7000
6990 NX=NX+NXV:GOTO 6980
7000 LOCATE Y,NX:COLOR PC(N(CL)),0:PRINT CHR$(L(N(CL))):GO2=1
7010 L=CL:GOSUB 7240:GOSUB 7020:GOTO 7070
7020  REM 
7030  REM @@@@ ERASE PROPERTY CARD @@@@
7040  REM 
7050 IF CD THEN CD=0
7060 GOSUB 9760:FOR A=T2+1 TO B2-1:LOCATE A,L2+1:PRINT SPACE$(32);:NEXT A:RETURN
7070  REM 
7080  REM @@@@ SET VARIABLES AGAIN @@@@
7090  REM 
7100 CP(N(CL))=CP
7110 X(1,N(CL))=NX
7120 Y(N(CL))=Y
7130 IF NM THEN 1360
7140 IF CB OR GO2 OR DB THEN 960
7150 IF CL=NUM_OF_PLAYERS THEN CP=CP(N(1)) ELSE CP=CP(N(CL+1))
7160 GOTO 960
7170  REM 
7180  REM @@@@ WHEN NOT ENOUGH MONEY @@@@
7190  REM 
7200 COLOR 30,1:L=PL:GOSUB 1430
7210 LA$="O6 B64 A64 G64 F64 B64 F64 C64":LB$="O5 A64 B64 C64 D64 E64 F64 C64"
7220 IF SOUNDS THEN PLAY "MF"+LA$+LA$+LA$+LB$+LA$+LB$+LA$+LB$
7230 L=PL:GOSUB 10310:GOSUB 1400:RETURN
7240  REM 
7250  REM @@@@ SHOW CURRENT PROPERTIES @@@@
7260  REM 
7270 BF=0:BP=CP(N(L)):LOCATE T6,BH(L):COLOR PC(N(L)),0:PSS=1/2*WH-1/2*LEN(PROPERTY_SHORT_NAMES$((BP))
7280 PRINT STRING$(INT(PSS+(K(BP)>1))-1,CHR$(205))"[";
7290 IF BP=10 AND J(N(L)) THEN COLOR 0,6:PRINT"JAIL"; ELSE GOSUB 10880
7300 COLOR PC(N(L)),0:PSS=POS(O)+1:PRINT"]"STRING$(BH(L)+WH-PSS,CHR$(205))
7310 RETURN
7320 REM erase menu
7330 COLOR 0,0:LOCATE T4,1:PRINT SPACE$(80);:RETURN
7340 COLOR 0,0:LOCATE T5,L2:PRINT SPACE$(32):RETURN
7350  REM 
7360  REM SEECARD
7370  REM 
7380 LASTCARD=0:IF SC=1 THEN COLOR 6,0:LOCATE T2,L2+14:PRINT "Chance" ELSE COLOR 3,0:LOCATE T2,L2+9:PRINT "Community Chest"
7390 COLOR 7,0:LOCATE B1+1,L2+3:PRINT "ENTER 2-DIGIT CARD # [01-16]";:INK$=""
7400 A$=INKEY$:IF LEN(A$)=0 THEN 7400
7410 IF VAL(A$) OR A$="0" THEN INK$=INK$+A$ ELSE IF A$=CHR$(27) THEN SEE=0:GOSUB 7330:GOSUB 7060:GOTO 3950 ELSE GOSUB 11280:GOTO 7400
7420 IF LEN(INK$)<2 THEN 7400 ELSE AA=VAL(INK$)
7430 IF AA>16 OR AA<1 THEN GOSUB 11280:INK$="":GOTO 7400
7440 CARD=AA:IF CARD=LASTCARD THEN LASTCARD=0:COLOR 0,0:GOSUB 11150:GOTO 7390 ELSE COLOR 0,9-3*SC:GOSUB 11150
7450 LASTCARD=CARD:GOSUB 7580:GOTO 7390
7460  REM 
7470  REM @@@@ SHOW CHANCE/COMMUNITY CHEST CARD @@@@
7480  REM 
7490 CD=1:PM=0:IF SC=2 THEN 7540
7500 COLOR 6,0:LOCATE T2,L2+15:PRINT"Chance":COLOR 0,6:GOSUB 11150
7510 IF NM=0 THEN C1=C1+1:IF C1>16 THEN C1=1
7520 IP=2:GOSUB 11180:IF IP=2 THEN C1(C1)=INK
7530 CARD=C1(C1):IF J1 AND CARD=5 THEN 7510 ELSE 7580
7540 COLOR 3,0:LOCATE T2,L2+10:PRINT "Community Chest":COLOR 0,3:GOSUB 11150
7550 IF NM=0 THEN C2=C2+1:IF C2>16 THEN C2=1
7560 IP=2:GOSUB 11180:IF IP=2 THEN C2(C2)=INK
7570 CARD=C2(C2):IF J2 AND CARD=14 THEN 7550
7580 COLOR 0,9-SC*3:FOR A=1 TO VAL(CARD$(SC,CARD,0))
7590 LOCATE T3+(7-VAL(CARD$(SC,CARD,0))-1)/2+A-1,L3+(30-LEN(CARD$(SC,CARD,A))+1)/2
7600 PRINT CARD$(SC,CARD,A);:NEXT A:RETURN
7610 RETURN
7620 REM
7630 REM @@@@ CARRY OUT CHANGES/EFFECTS OF CARDS @@@@
7640 REM
7650 WW=2*WW:GOSUB 10310:WW=WW/2
7660 GOSUB 7060
7670 IF PROPERTY_NAMES$(CP)="COMMUNITY CHEST" THEN 7890
7680 ON C1(C1) GOTO 7690,7700,7710,7720,7730,7740,7750,7760,7770,7780,7790,7800,7840,7850,7860,7870
7690 PA=50:GOTO 8100
7700 AC2=1:ALL=1:PA=-50:PB=50:GOTO 8100
7710 PT=1:IF CP=7 THEN CP=12:GOTO 8120 ELSE IF CP=22 THEN CP=28:GOTO 8120 ELSE CP=12:GOTO 8110
7720 BW=1:DR=3:GOTO 8150
7730 FJ(N(CL))=FJ(N(CL))+1:J1=N(CL):GOTO 8130
7740 IF CP=36 THEN CP=24:GOTO 8110 ELSE CP=24:GOTO 8120
7750 CP=39:GOTO 8120
7760 PT=1:IF CP=7 THEN CP=15:GOTO 8120 ELSE IF CP=22 THEN CP=25:GOTO 8120 ELSE CP=5:GOTO 8110
7770 IF CP=7 THEN CP=11:GOTO 8120 ELSE CP=11:GOTO 8110
7780 PA=-15:GOTO 8100
7790 CP=0:GOTO 8110
7800 FOR A=0 TO P(N(CL))-1
7810 IF U(PP(N(CL),A))=5 THEN PA=PA-100
7820 IF U(PP(N(CL),A))<5 THEN PA=PA-(25*(U(PP(N(CL),A))))
7830 NEXT A:GOTO 8100
7840 PA=150:GOTO 8100
7850 GOTO 8140
7860 CP=5:GOTO 8110
7870 PT=1:IF CP=7 THEN CP=15:GOTO 8120 ELSE IF CP=22 THEN CP=25:GOTO 8120 ELSE CP=5:GOTO 8110
7880 REM
7890 ON C2(C2) GOTO 7900,7910,7920,7930,7940,7950,7960,7970,7980,7990,8000,8010,8050,8060,8070,8080
7900 PA=10:GOTO 8100
7910 PA=45:GOTO 8100
7920 PA=-150:GOTO 8100
7930 PA=200:GOTO 8100
7940 GOTO 8140
7950 CP=0:GOTO 8110
7960 PA=-50:GOTO 8100
7970 PA=25:GOTO 8100
7980 PA=100:GOTO 8100
7990 PA=100:GOTO 8100
8000 PA=200:GOTO 8100
8010 FOR A=0 TO P(N(CL))-1
8020 IF U(PP(N(CL),A))=5 THEN PA=PA-115
8030 IF U(PP(N(CL),A))<5 THEN PA=PA-(40*(U(PP(N(CL),A))))
8040 NEXT A:GOTO 8100
8050 PA=-100:GOTO 8100
8060 FJ(N(CL))=FJ(N(CL))+1:J2=N(CL):GOTO 8130
8070 PA=20:GOTO 8100
8080 AC2=1:ALL=1:PA=50:PB=-50:GOTO 8100
8090 REM
8100 CP(N(CL))=CP:GOSUB 6590:GOTO 7010       'MONEY
8110 GOSUB 11570:CP(N(CL))=CP:GOTO 6950      'GO2[+$200]
8120 GOSUB 11570:CP(N(CL))=CP:GOTO 6960      'GO2
8130 CP(N(CL))=CP:L=CL:GOSUB 1540:GOTO 7010  'FREE CARDS
8140 CP(N(CL))=CP:GOTO 6870                  'JAIL
8150 CP(N(CL))=CP:GOTO 4590                  'BACK 3
8160 REM
8170 REM @@@@ Eliminate Player @@@@
8180 REM
8190 O$="Eliminate Player #"+STR$(L)+"-":O$(1)="yes":O$(2)="no":O=2:GOSUB 10000:IF AA<>1 THEN GOSUB 7330:RETURN
8200 FOR P=1 TO 39:IF O(P)=N(L) THEN O(P)=0:M(P)=0
8210 NEXT P
8220 FJ(N(PL))=0:IF J1=N(L) THEN J1=0
8230 IF J2=N(L) THEN J2=0
8240 LL2=N(L):FOR LL1=L TO NUM_OF_PLAYERS-1:N(LL1)=N(LL1+1):NEXT LL1:N(NUM_OF_PLAYERS)=LL2
8250 FOR LL1=1 TO 4:UN(N(LL1))=LL1:NEXT LL1
8260 NUM_OF_PLAYERS=NUM_OF_PLAYERS-1
8270 IF L<CL THEN CL=CL-1 ELSE IF CL>NUM_OF_PLAYERS THEN CL=1
8280 IF L=CL THEN DB=0:PT=0
8290 CP=CP(N(CL)):SG=1:FM=1:IF NM AND PL1=L THEN NM=0
8300 RETURN
8310 REM
8320 REM *DUE*
8330 GOSUB 10360
8340 LOCATE B1,L2+11:COLOR 14,1:PRINT USING"[Due**$###]";DUE:RETURN
8350 REM
8360 REM **** SAVE GAME IN GN$ ****
8370 LOCATE B1+1,L2+7:COLOR 7,0:PRINT USING"Saving:`&'";GN$
8380 OPEN "O",#1,GN$+".MF":WRITE #1,CL,CP,NX,Y,DB,NUM_OF_PLAYERS,J1,J2,T,E,C1,C2,WW,SOUNDS,NM,PA,PB,PL,PL1,PL2,AC1,AC2,PLA,PLB,ALL,NOROLL,D1,D2,PT,PLAYY
8390 FOR J=1 TO 4:WRITE #1,J$(J),J(J),L(J),CP(J),X(1,J),Y(J),W(J),PC(J),FJ(J),RTP(J),TP(J),N(J),UN(J),OCC3(J),OCCR(J):NEXT J
8400 FOR P=1 TO 16:WRITE #1,C1(P),C2(P):NEXT P
8410 FOR P=1 TO 40:WRITE #1,O(P),U(P),M(P):NEXT P:CLOSE #1:GOSUB 7330:RETURN
8420 REM
8430 REM **** LOAD GAME FROM GN$ ****
8440 LOCATE B1+1,L2+7:COLOR 7,0:PRINT USING"Loading:`&'";GN$
8450 OPEN "I",#1,GN$+".MF":INPUT #1,CL,CP,NX,Y,DB,NUM_OF_PLAYERS,J1,J2,T,E,C1,C2,WW,SOUNDS,NM,PA,PB,PL,PL1,PL2,AC1,AC2,PLA,PLB,ALL,NOROLL,D1,D2,PT,PLAYY
8460 FOR J=1 TO 4:INPUT #1,J$(J),J(J),L(J),CP(J),X(1,J),Y(J),W(J),PC(J),FJ(J),RTP(J),TP(J),N(J),UN(J),OCC3(J),OCCR(J):NEXT J
8470 FOR P=1 TO 16:INPUT #1,C1(P),C2(P):NEXT P
8480 FOR P=1 TO 40:INPUT #1,O(P),U(P),M(P):NEXT P:CLOSE #1:GOSUB 7330:RETURN
8490 REM
8500 REM DISPLAY PLAYER'S CHARACTER
8510 CAB=SB
8520 COLOR PC(N(CA)),0
8530 IF BN(CP(N(CA)))=CAB THEN XN(N(CA))=1:GOTO 8550
8540 IF K(CP(N(CA)))=0 AND BN(CP(N(CA)))=CAB MOD 4+1 THEN XN(N(CA))=2 ELSE 8560
8550 LOCATE Y(N(CA)),X(XN(N(CA)),N(CA)):IF EB THEN PRINT " " ELSE PRINT CHR$(L(N(CA)))
8560 RETURN
8570 REM **** POSITION PLAYERS' BOARD
8580 L1=3:R1=L1+74:T1=1:B1=T1+5
8590 IF NUM_OF_PLAYERS=1 THEN L2=4:BH(1)=(80-9+33+L2)/2
8600 IF NUM_OF_PLAYERS=2 THEN L2=24:BH(1)=(L2-9)/2:BH(2)=(L2-9)/2+L2+33
8610 IF NUM_OF_PLAYERS=3 THEN L2=4:BH(1)=(80+L2+33-9)/3+4:BH(2)=(80+L2+33-9)/2+1:BH(3)=(80+L2+33-9)*2/3-2
8620 IF NUM_OF_PLAYERS=4 THEN L2=24:BH(1)=3:BH(2)=BH(1)+11:BH(3)=L2+35:BH(4)=BH(3)+11
8630 T2=B1+2:B2=T2+16:R2=L2+33
8640 T3=T2+4:B3=T3+6:L3=L2+1:R3=R2-1
8650 T4=B1+1:T5=B1+2:W6=9:H6=14:W#=14:T6=B1+2:B6=T6+H6-1
8660 FOR L=1 TO 4:BV(L)=T6+3:NEXT L
8670 RETURN
8680 REM
8690 REM save file names
8700 OPEN "O",#1,"M.FF":FOR A=0 TO GN:WRITE #1,GN$(A):NEXT A:CLOSE #1:RETURN
8710 REM
8720 REM load file names
8730 OPEN "I",#1,"M.FF":FOR A=0 TO 59
8740 IF EOF(1) THEN GN=A-1 ELSE INPUT #1,GN$(A):NEXT A
8750 CLOSE #1:IF GN=-1 THEN SG=0:RETURN ELSE SG=1:RETURN
8760 REM
8770 REM choose from saved game list
8780 POKE 1050,PEEK(1052)
8790 SCREEN 0,,1,0:COLOR ,0:CLS:SCREEN 0,,1,1
8800 COLOR ,0:CLS:SCREEN 0,,1,1
8810 X1=33:X2=43:IF GN>19 THEN X1=28:X2=51:IF GN>39 THEN X1=26:X2=62
8820 COLOR 7,0:LOCATE 2,X1-1:PRINT"É";+STRING$(X2-X1+1,CHR$(205))+"»";
8830 FOR A=0 TO GN:LOCATE A+3,X1-1:PRINT"º";:LOCATE ,X2+1:PRINT "º";:NEXT A
8840 LOCATE A+3,X1-1:PRINT"È"+STRING$(X2-X1+1,CHR$(205))+"¼";
8850 COLOR 7,0:FOR A=0 TO GN:YY=A MOD 20+3:XX=X1+FIX(A/20)*13:LOCATE YY,XX:PRINT USING"##)";A+1;:PRINT GN$(A)+SPACE$(8-LEN(GN$(A)));:NEXT:A=0 'FOR A=YY+1 TO 22:LOCATE A,XX:PRINT SPACE$(11):NEXT A:A=0
8860 YY=A MOD 20+3:XX=X1+FIX(A/20)*13:COLOR 14,1:LOCATE YY,XX:PRINT USING"##)";A+1;:PRINT GN$(A)+SPACE$(8-LEN(GN$(A)));
8870 A$=INKEY$:ON LEN(A$)+1 GOTO 8870,8880,8900
8880 IF A$=CHR$(13) THEN 8970
8890 IF A$=CHR$(27) THEN SG=0:SCREEN 0,,0,0:RETURN ELSE 8870
8900 A$=RIGHT$(A$,1):IF A$=CHR$(83) THEN KILL GN$(A)+".MF":FOR C=A TO GN:GN$(C)=GN$(C+1):NEXT C:GN=GN-1:GOSUB 8680:IF GN<0 THEN A$=CHR$(27):GOTO 8890 ELSE 8800
8910 COLOR 7,0:LOCATE YY,XX:PRINT USING"##)";A+1;:PRINT GN$(A)+SPACE$(8-LEN(GN$(A)));
8920 IF A$=CHR$(80) THEN IF A=GN OR (A+1) MOD 20=0 THEN IF GN>0 THEN A=INT((A-1)/20)*20 ELSE A=0 ELSE A=A+1
8930 IF A$=CHR$(72) THEN IF A MOD 20=0 THEN IF A+19>GN THEN A=GN ELSE A=INT(A/20+1)*20-1 ELSE A=A-1
8940 IF A$=CHR$(75) THEN IF A<20 THEN IF A<=GN MOD 20 THEN A=A+INT(GN/20)*20 ELSE A=A+INT(GN/20-1)*20 ELSE A=A-20
8950 IF A$=CHR$(77) THEN IF A>GN-20 THEN A=A MOD 20 ELSE A=A+20
8960 GOTO 8860
8970 GN$=GN$(A):COLOR ,0:CLS:SCREEN 0,,0,1:CLS:SCREEN 0,,0,0:RETURN
8980 REM
8990 REM ERRORS
9000 COLOR 14,4:LOCATE T2+1,L2+1
9010 GOSUB 10310:COLOR 0,0:LOCATE T2+1,L2+1:PRINT SPACE$(31):RETURN
9020 REM
9030 REM ERASE GAME FILE
9040 FOR A=0 TO 59:IF GN$<>GN$(A) THEN NEXT A
9050 FOR A=A TO 59:GN$(A)=GN$(A+1):NEXT A:GN=GN-1:KILL GN$+".MF":RETURN
9060 REM
9070 REM ACCEPT INFORMATION
9080 KYL=8:KYTF=7:KYF=14:KYB=1:KYFSTR$="\"+SPACE$(KYL-2)+"\":KYA=1:KYY=B1+1:KY$=STRING$(KYL,97)
9090 KYFSTR$="\"+SPACE$(KYL-2)+"\":KY$=SPACE$(KYL)
9100 INS=0:KYX=L2+(32-LEN(KYT$)-KYL)/2:COLOR KYTF,0:LOCATE KYY,KYX:PRINT KYT$;:KYX=KYX+LEN(KYT$)
9110 KY=1
9120 LOCATE KYY,KYX:COLOR KYF,KYB:PRINT USING KYFSTR$;K$
9130 IF KY>KYL THEN KY=KYL ELSE IF KY<1 THEN KY=1
9140 IF INS THEN LOCATE ,,,4,7 ELSE LOCATE ,,,7,0
9150 KY$=INKEY$
9160 LOCATE KYY,KYX+KY-1,1
9170 IF LEN(KY$) THEN 9180 ELSE 9150
9180 LOCATE ,,0
9190 IF KY$=CHR$(13) THEN 9380
9200 IF KY$=CHR$(9) THEN IF KY=1 THEN KY=KYL:GOTO 9120 ELSE KY=1:GOTO 9120
9210 IF KY$=CHR$(27) THEN GOSUB 7330:K$="":RETURN
9220 IF LEN(KY$)<>2 OR RIGHT$(KY$,1)<>CHR$(83) THEN 9240
9230 MID$(K$,KY,KYL-KY+1)=MID$(K$,KY+1,KYL-KY)+" ":GOTO 9120
9240 IF LEN(KY$)<>2 OR RIGHT$(KY$,1)<>CHR$(82) THEN 9270
9250 IF INS=0 THEN INS=1 ELSE INS=0
9260 GOTO 9120
9270 IF LEN(KY$)<>2 OR RIGHT$(KY$,1)<>CHR$(75) THEN 9290
9280 KY=KY-1:GOTO 9120
9290 IF LEN(KY$)<>1 OR KY$<>CHR$(8) THEN 9310
9300 KY=KY-1:GOTO 9230
9310 IF LEN(KY$)<>2 OR RIGHT$(KY$,1)<>CHR$(77) THEN 9330
9320 KY=KY+1:GOTO 9120
9330 IF LEN(KY$)<>1 OR (KYA=2 AND KY$<>"0" AND VAL(KY$)=0) THEN 9120
9340 IF INS THEN 9360 ELSE IF LEN(K$)<KY THEN K$=K$+KY$ ELSE MID$(K$,KY,1)=KY$
9350 KY=KY+1:GOTO 9120
9360 K$=LEFT$(K$,KY-1)+KY$+LEFT$(RIGHT$(K$,KYL-KY+1),KYL-KY)
9370 KY=KY+1:GOTO 9120
9380 GOSUB 7330:RETURN
9390 REM
9400 REM checking routine for a filename
9410 DUP=0:IF K$<>SPACE$(8) AND K$<>"" THEN GOSUB 9540:GOSUB 9490:OK$=K$:GOSUB 9580 ELSE DUP=1:RETURN
9420 IF K$<>OK$ THEN GOSUB 9060:GOTO 9400 ELSE DUP=0
9430 GOSUB 7330:GOSUB 9450:IF DUP THEN GOSUB 9860:IF DUP THEN RETURN ELSE GOSUB 8680:RETURN
9440 GN=GN+1:GN$(GN)=K$:GN$=K$:GOSUB 9790:GOSUB 8680:RETURN
9450 REM
9460 REM SEARCH FOR DUPLICATE FILES
9470 DUP=0:FOR A=0 TO GN:IF K$=GN$(A) THEN DUP=1
9480 NEXT A:RETURN
9490 REM
9500 REM ERASE SPACES IN FILE NAME
9510 H=LEN(K$):IF RIGHT$(K$,1)=CHR$(32) THEN K$=LEFT$(K$,H-1):GOTO 9510
9520 H=LEN(K$):IF LEFT$(K$,1)=CHR$(32) THEN K$=RIGHT$(K$,H-1):GOTO 9520
9530 RETURN
9540 REM
9550 REM capitalize file names
9560 FOR A=1 TO LEN(K$):A$=MID$(K$,A,1):IF ASC(A$)=>97 AND ASC(A$)<=122 THEN MID$(K$,A,1)=CHR$(ASC(A$)-32)
9570 NEXT A:RETURN
9580 REM
9590 REM replace spaces with underscore chr's
9600 FOR A=1 TO LEN(K$):AA=ASC(RIGHT$(K$,A))
9610 IF AA=33 OR (AA>34 AND AA<42) OR AA=45 OR (AA>47 AND AA<58) OR (AA>63 AND AA<91) OR (AA>93 AND AA<97) OR AA=123 OR AA=125 THEN 9640
9620 IF AA=32 THEN IF RIGHT$(K$,A)=SPACE$(A) THEN 9640
9630 K$=LEFT$(K$,LEN(K$)-A)+"_"+RIGHT$(K$,A-1)
9640 NEXT A:RETURN
9650 REM
9660 REM END/SAVE GAME OPTIONS
9670 NW=0:ED=0:DUP=0:O$="":OO(0)=1:O$(1)="save":OO(1)=1:O$(2)="end":OO(2)=1:O$(3)="new":OO(3)=1:O$(4)="load":GOSUB 8710:IF SG THEN OO(4)=1 ELSE OO(4)=0
9680 O=4:GOSUB 9960
9690 ON AA+1 GOTO 9710,9720,9740,9750,9700
9700 GOSUB 8760:IF SG THEN GOSUB 8430:CGN$=GN$:GOSUB 9020:GOSUB 8690:GOSUB 750:FM=1:RETURN 890 ELSE 9660
9710 NW=0:ED=0:RETURN
9720 GOSUB 8710:KYT$="File name:":K$=CGN$:GOSUB 9060:GOSUB 9400:IF DUP=0 THEN GN$=K$:GOSUB 8350:CGN$=GN$
9730 GOTO 9650
9740 COLOR ZZ,0:CLS:ED=1:NW=0:RETURN
9750 NW=1:ED=0:RETURN
9760 REM
9770 REM blank TOP OF CARD BOX
9780 LOCATE T2,L2+1:COLOR ZZ,0:PRINT STRING$(32,CHR$(205));:RETURN
9790 REM
9800 REM Alphabetize FILE NAMES
9810 D=GN
9820 D=INT(D/5)+1:FOR I=0 TO GN-D:FOR J=I TO 0 STEP -D:IF GN$(J)>GN$(J+D) THEN SWAP GN$(J),GN$(J+D):NEXT
9830 NEXT I
9840 IF D>1 THEN GOTO 9820
9850 RETURN
9860 REM
9870 REM REPLACE EXISTING GAME FILE WITH K$??
9880 O$="Replace old `"+K$+"'-":O$(1)="yes":O$(2)="no":O=2:GOSUB 10000
9890 IF AA=1 THEN DUP=0:RETURN ELSE DUP=1:RETURN
9900 REM
9910 REM menus
9920 REM
9930 REM O$=Text:O$()=Options 1-5:O=# of options:a=foreground color:b=background color:c=highlighting color:d=background highlight color:x1=center x pos:y1=verticle pos:aa=current option #
9940 AA=1
9950 Y1=T4:X1=L2+16:A=PC(N(PL)):B=0:C=14:D=1:GOTO 10020
9960 AA=1
9970 Y1=T4:X1=L2+16:A=7:B=0:C=14:D=1:GOTO 10020
9980 AA=1
9990 Y1=T4:X1=L2+16:A=PC(N(PL)):B=0:C=14:D=1:FOR F=0 TO O:OO(F)=1:NEXT F:GOTO 10020
10000 AA=1
10010 Y1=T4:X1=L2+16:A=7:B=0:C=14:D=1:FOR F=0 TO O:OO(F)=1:NEXT F
10020 IF INKEY$<>"" THEN GOSUB 10350
10030 DEF SEG=0:POKE 1050,PEEK(1052)
10040 XX=LEN(O$):FOR F=1 TO O:XX=XX+LEN(O$(F))+3:NEXT F:XX=X1-INT((XX-3)/2)
10050 IF XX<1 THEN XX=1
10060 COLOR A,B:LOCATE Y1,XX:PRINT O$+"[";:FOR FF=1 TO O-1:GOSUB 10270:PRINT O$(FF)+" | ";:NEXT FF:GOSUB 10270:PRINT O$(O)+"]";:F=0
10070 IF OO(AA)=0 THEN IF AA<O THEN AA=AA+1:GOTO 10070
10080 IF AA>O THEN IF F=1 THEN AA=0:RETURN ELSE AA=1:F=1:GOTO 10070
10090 XXX=XX+LEN(O$)+1:LOCATE Y1,XXX:FOR F=1 TO O:IF AA=F THEN COLOR C,D:PRINT OO$(F); ELSE IF OO(F)=0 THEN COLOR ZZ,B:PRINT O$(F) ELSE COLOR A,B:PRINT O$(F);
10100 XXX=XXX+LEN(O$(F))+3:LOCATE Y1,XXX:NEXT
10110 IF AUTOKYBD THEN A$=CHR$(13) ELSE A$=INKEY$:ON LEN(A$)+1 GOTO 10110,10120,10150
10120 IF A$=CHR$(13) THEN 10230
10130 IF A$=CHR$(27) AND OO(0)=1 THEN AA=0:GOTO 10230
10140 FOR F=1 TO O:IF (ASC(A$)=ASC(O$(F)) OR ASC(A$)=ASC(OO$(F))) AND OO(F) THEN AA=F:GOTO 10230 ELSE NEXT:GOTO 10110
10150 A$=RIGHT$(A$,1):IF A$<>CHR$(77) THEN  10180
10160 IF AA=O THEN AA=1 ELSE AA=AA+1
10170 IF OO(AA)=0 THEN 10160 ELSE 10090
10180 IF A$<>CHR$(75) THEN 10210
10190 IF AA=1 THEN AA=O ELSE AA=AA-1
10200 IF OO(AA)=0 THEN 10190 ELSE 10090
10210 IF ASC(A$)=>120 AND ASC(A$)<=129 THEN WW=(ASC(A$)-120):GOTO 10110
10220 IF A$=CHR$(79) THEN GOSUB 7330:GOSUB 9650:IF ED THEN END ELSE 10 ELSE 10110
10230 XXX=XX+LEN(O$)+1:LOCATE Y1,XXX:FOR F=1 TO O:IF AA=F THEN COLOR C,D:PRINT OO$(F); ELSE IF OO(F)=0 THEN COLOR ZZ,B:PRINT O$(F) ELSE COLOR A,B:PRINT O$(F);
10240 XXX=XXX+LEN(O$(F))+3:LOCATE Y1,XXX:NEXT
10250 FOR F=1 TO O:O$(F)="":OO(F)=0:NEXT:O$="":GOSUB 7330:RETURN
10260 REM
10270 REM Capitalizing
10280 OO$(FF)=O$(FF):OO=LEN(OO$(FF)):FOR F=1 TO OO:A$=RIGHT$(OO$(FF),F):IF ASC(A$)=>97 AND ASC(A$)<=122 THEN OO$(FF)=LEFT$(OO$(FF),(OO-F))+CHR$(ASC(A$)-32)+RIGHT$(A$,F-1)
10290 NEXT F:RETURN
10300 REM
10310 REM PAUSE
10320 FOR W=1 TO WW:A$=TIME$:WHILE A$=TIME$:IF LEN(INKEY$) THEN GOSUB 10350 ELSE WEND:NEXT W
10330 RETURN
10340 REM
10350 REM TURN AUTOMATIC KEYBOARD OFF
10360 AUTOKYBD=0:GOSUB 11610:RETURN
10370 REM
10380 REM TURN AUTOMATIC KEYBOARD ON
10390 AUTOKYBD=1:GOSUB 11610:RETURN
10400 REM
10410 REM `other' menu
10420 O$="":O$(1)="see":O$(2)="file":O$(3)="options":O$(4)="defaults":O=4
10430 GOSUB 10000
10440 ON AA+1 GOTO 1940,3910,10650,10830,10720
10450 GOSUB 10380:GOTO 1940
10460 REM
10470 REM SET NUMBER OF PLAYERS
10480 COLOR 7,0:LOCATE B1+1,L2+2:PRINT "Enter number of players [1-4]";
10490 GOSUB 11350:IF INK$=CHR$(27) THEN GOSUB 7330:GOTO 10830
10500 IF INK>4 OR INK<1 THEN GOSUB 11280:GOTO 10490
10510 GOSUB 7330:COLOR 7,0:LOCATE B1+1,L2+12:PRINT USING "# Players";INK
10520 GOSUB 10310:GOSUB 7330
10530 IF NUM_OF_PLAYERS=INK GOTO 10410 ELSE IF INK>NUM_OF_PLAYERS THEN 10550
10540 FOR L=NUM_OF_PLAYERS TO INK+1 STEP -1:GOSUB 8200:NEXT L
10550 FOR L=NUM_OF_PLAYERS+1 TO INK:GOSUB 11410:NEXT L
10560 NUM_OF_PLAYERS=INK:GOSUB 750:SG=1:FM=1:GOTO 890
10570 REM
10580 REM SET PAUSE VALUE
10590 COLOR 7,0:LOCATE B1+1,L2+5:PRINT "Enter Pause value [0-9]";
10600 GOSUB 11350:IF INK$=CHR$(27) THEN 10620 ELSE WW=INK
10610 GOSUB 7330:COLOR 7,0:LOCATE B1+1,L2+8:PRINT USING "# Second Pauses";WW
10620 GOSUB 10310:GOSUB 7330:GOTO 10720
10630 REM
10640 REM files
10650 GOSUB 9650:IF ED THEN END ELSE IF NW THEN 10 ELSE 10410
10660 REM delete player
10680 GOSUB 12161:IF EPN=0 THEN 10830
10700 L=EPN:GOSUB 8160:IF AA=1 THEN GOSUB 750:SG=1:FM=1:GOTO 890 ELSE 10830
10710 REM
10720 REM `defaults' menu
10730 O$="":OO(0)=1:O$(1)="pause("+RIGHT$(STR$(WW),1)+")":O$(2)="auto":O=4:OO(1)=1:OO(3)=1:OO(4)=1:IF NM=0 THEN OO(2)=1 ELSE OO(2)=0
10740 IF SOUNDS THEN O$(3)="sound(on)" ELSE O$(3)="sound(off)"
10750 IF ZZ=7 THEN O$(4)="monitor(monochrome)" ELSE O$(4)="monitor(color)"
10760 GOSUB 9960:ON AA+1 GOTO 10410,10580,10450,10870,10770
10770 REM
10780 REM change monitor
10790 COLOR 7,0:LOCATE B1+1,L2+7:IF ZZ=7 THEN PRINT "  Color Monitor   ":ZZ=8 ELSE PRINT "Monochrome Monitor":ZZ=7
10800 FC(12)=ZZ:FC(28)=ZZ:FC(4)=ZZ
10810 GOSUB 10300:GOSUB 7330:GOSUB 750:SG=1:FM=1:GOTO 890
10820 REM
10830 REM `options' menu
10840 O$="":O$(1)="players":O$(2)="delete":O$(3)="token":O$(4)="name":O=4:OO(0)=1:OO(1)=1:OO(3)=1:OO(4)=1:IF NUM_OF_PLAYERS>1 THEN OO(2)=1 ELSE OO(2)=0
10850 GOSUB 9960:ON AA+1 GOTO 10410,10460,10660,10860,12140
10860 GOSUB 170:IF AA=1 THEN GOSUB 750:SG=1:FM=1:GOTO 890 ELSE 10830
10870 GOSUB 11240:GOTO 10720
10880 REM
10890 REM property symbols
10900 REM
10910 MPC=ZZ-M(BP)
10920 IF K(BP)<2 THEN 10990
10930 IF BF THEN COLOR 0,0:PRINT SPACE$((BF-(LEN(PROPERTY_SHORT_NAMES$((BP))+2))/2);
10940 IF GN(BP)>8 THEN 10960 ELSE COLOR FC(BP),BC(BP)
10950 PRINT "±";:COLOR MPC,0:PRINT PROPERTY_SHORT_NAMES$((BP);:COLOR FC(BP),BC(BP):PRINT"±";:GOTO 10970
10960 CR$=CHR$(219+131*(GN(BP)=9)):COLOR 7,0:PRINT CR$;:COLOR MPC,0:PRINT PROPERTY_SHORT_NAMES$((BP);:COLOR 7,0:PRINT CR$;
10970 IF BF THEN COLOR 0,0:PRINT SPACE$((BF-(LEN(PROPERTY_SHORT_NAMES$((BP))+2))/2);
10980 GOTO 11020
10990 IF BF THEN COLOR 0,0:PRINT SPACE$((BF-LEN(PROPERTY_SHORT_NAMES$((BP)))/2);
11000 IF GN(BP)=10 THEN COLOR MPC,0 ELSE COLOR FC(BP),0
11010 PRINT PROPERTY_SHORT_NAMES$((BP);:IF BF THEN COLOR 0,0:PRINT SPACE$((BF-LEN(PROPERTY_SHORT_NAMES$((BP)))/2);
11020 RETURN
11030 REM
11040 REM show houses
11050 COLOR 2,0:ON CU(BP)+1 GOTO 11110,11060,11070,11080,11090,11100
11060 PRINT "   ";:GOTO 11120
11070 PRINT "  ";:GOTO 11120
11080 PRINT " ";:GOTO 11120
11090 PRINT "";:GOTO 11120
11100 COLOR 4,0:PRINT "   ";:GOTO 11120
11110 PRINT "    ";
11120 RETURN
11130 REM
11140 REM color/erase chance/chest card area
11150 FOR A=T3 TO B3:LOCATE A,L2+1:PRINT SPACE$(32):NEXT A:RETURN
11160 REM
11170 REM hidden input
11180 A$=INKEY$:IF LEN(A$) THEN GOSUB 10350
11190 IF A$<>CHR$(8) THEN IP=0:GOTO 11230
11200 INK=0:FOR F=1 TO IP
11210 A$=INKEY$:IF VAL(A$) OR A$="0" THEN INK=INK+VAL(A$) ELSE 11210
11220 NEXT F
11230 RETURN
11240 REM sound on/off
11250 COLOR 7,0:LOCATE T4,L2+10:IF SOUNDS THEN SOUNDS=0:PRINT "Sound Off" ELSE SOUNDS=1:PRINT "Sound On"
11260 GOSUB 10300:GOSUB 7330
11270 RETURN
11280 REM beeps
11290 IF SOUNDS THEN BEEP
11300 RETURN
11310 REM
11320 REM put back bottom of board
11330 COLOR ZZ,0:LOCATE T1+5,L1:PRINT"ÈÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍ¼":RETURN
11340 REM
11350 REM GET NUMBER
11360 INK$=INKEY$:IF LEN(INK$)=0 THEN 11360
11370 IF LEN(INK$)<>1 THEN GOSUB 11280:GOTO 11360 ELSE IF INK$=CHR$(27) THEN INK=0:RETURN
11380 IF (VAL(INK$) OR INK$="0") THEN INK=VAL(INK$) ELSE GOSUB 11280:GOTO 11360
11390 RETURN
11400 REM
11410 REM CLEAR A NEW PLAYER'S VARIABLES
11420 TP(N(L))=0:RTP(N(L))=0:CP(N(L))=0:X(1,N(L))=R1-5+L:Y(N(L))=T1+4:W(N(L))=1500:RETURN
11430 REM
11440 REM IF NM IN A LOADED GAME GO TO CORRECT NEED-MONEY ROUTINE TO PAY DEPT
11450 IF PLAYY=1 OR PLAYY=2 THEN SC=PLAYY:GOSUB 7460:GOTO 11490
11460 IF PLAYY=8 THEN RL=0:P1=CP:GOSUB 5020:RL=1:GOSUB 5020:GOTO 11490
11470 IF PLAYY=9 THEN 5800
11480 IF PLAYY=11 THEN SC=1:GOSUB 7460:GOSUB 10310:GOSUB 7060:GOTO 5800
11490 ON NM GOTO 6050,1280,11500,2220,11510
11500 GOSUB 6660:GOTO 7010
11510 IF ALL THEN GOSUB 6640:GOTO 7010 ELSE GOSUB 6700:GOTO 7010
11520 REM
11530 REM HIGHLIGHT/NORMALIZE `DOUBLES' AT TOP OF BOARD
11540 LOCATE T1,L1+11:COLOR ZZ,0:PRINT "[";:IF DB THEN COLOR 14,1:PRINT "DOUBLES"; ELSE PRINT "doubles";
11550 COLOR ZZ,0:PRINT "]":RETURN
11560 REM
11570 REM HIGHLIGHT/NORMALIZE `DOUBLE RENT' AT TOP OF BOARD
11580 LOCATE T1,R1-23:COLOR ZZ,0:PRINT "[";:IF PT THEN COLOR 14,1:PRINT "DOUBLE RENT"; ELSE PRINT "double rent";
11590 COLOR ZZ,0:PRINT "]";:RETURN
11600 REM
11610 REM HIGHLIGHT/NORMALIZE AUTO KEYBOARD
11620 LOCATE T1,L1+32:COLOR ZZ,0:PRINT "[";:IF AUTOKYBD THEN COLOR 14,1:PRINT "AUTOMATIC"; ELSE PRINT "automatic";
11630 COLOR ZZ,0:PRINT "]";:RETURN
11640 REM
11650 REM part one of deal menu
11660 IF NM THEN IF W(N(PL1))=>DUE THEN PL=PL1:GOSUB 7330:NM=0:GOSUB 11320:RETURN ELSE PL=PL1
11670 GV=PL:GOSUB 12120
11680 O$="":O$(1)="property":O$(2)="cards":O$(3)="money":O$(5)="bail":O=5:OO(0)=1:IF P(N(PL))=0 THEN OO(1)=0 ELSE OO(1)=1
11690 IF FJ(N(PL))>0 AND NUM_OF_PLAYERS>1 THEN OO(2)=1 ELSE OO(2)=0
11700 IF W(N(PL))>0 AND NUM_OF_PLAYERS>1 THEN OO(3)=1 ELSE OO(3)=0
11710 IF NUM_OF_PLAYERS>2 THEN OO(4)=1 ELSE OO(4)=0
11720 IF J(N(PL))=0 OR NM=4 THEN OO(5)=0 ELSE OO(5)=1
11730 O$(4)="receiver("+RIGHT$(STR$(RV),1)+")":GOSUB 9940:ON AA+1 GOTO 11750,2310,11770,11970,11740,2160
11740 GOSUB 11910:GOTO 11680
11750 GOTO 1940
11760 REM
11770 REM trade cards
11780 O$="":OO(0)=1:O$(1)="chance":O$(2)="community chest":O$(3)="receiver("+RIGHT$(STR$(RV),1)+")":O=3:IF J1=N(PL) THEN OO(1)=1 ELSE OO(1)=0
11790 IF J2=N(PL) THEN OO(2)=1 ELSE OO(2)=0
11791 IF NUM_OF_PLAYERS>2 THEN OO(3)=1 ELSE OO(3)=0
11800 GOSUB 9940:CARD=AA:IF AA=3 THEN GOSUB 11910:GOTO 11770 ELSE IF AA=0 THEN 11680
11810 O$="":O$(1)="sell":O$(2)="give":O$(3)="receiver("+RIGHT$(STR$(RV),1)+")":O=3:OO(1)=ABS((W(N(RV))>0)):OO(2)=1:IF NUM_OF_PLAYERS>2 THEN OO(3)=1 ELSE OO(3)=0
11820 GOSUB 9940:IF AA=3 THEN GOSUB 11910:GOTO 11770 ELSE IF AA=0 THEN 11780
11830 IF AA=2 THEN 11870
11840 K$="":KYT$="Sell for:":KYL=7:KYTF=PC(N(PL)):KYF=14:KYB=1:KYFSTR$="\     \":KYA=2:KYY=B1+1:GOSUB 9100
11850 IF KY$=CHR$(27) OR VAL(K$)=0 THEN 11810 ELSE IF VAL(K$)>W(N(RV)) THEN GOSUB 11280:GOTO 11840
11860 PA=VAL(K$):PB=-VAL(K$):AC1=GV:AC2=RV:GOSUB 6590
11870 FJ(N(GV))=FJ(N(GV))-1:FJ(N(RV))=FJ(N(RV))+1:IF CARD=1 THEN J1=N(RV) ELSE J2=N(RV)
11880 L=PL:GOSUB 1550:L=RV:GOSUB 1550
11890 IF NM=0 THEN 1940 ELSE 11650
11900 REM
11910 REM change receiver
11920 COLOR PC(N(GV)),0:LOCATE B1+1,L2+3:PRINT USING "Enter player number [1-#]";NUM_OF_PLAYERS
11930 A$=INKEY$:IF LEN(A$)=0 THEN 11930
11940 IF LEN(A$)<>1 THEN GOSUB 11280:GOTO 11930 ELSE IF A$=CHR$(27) THEN GOSUB 7330:RETURN
11950 IF VAL(A$)>0 AND VAL(A$)<=NUM_OF_PLAYERS AND VAL(A$)<>GV THEN RV=VAL(A$) ELSE GOSUB 11280:GOTO 11930
11960 GOSUB 7330:RETURN
11970 REM
11980 REM TRANFER FUNDS OVER TO ANOTHER PLAYER
11990 K$="":KYT$="Amount to trade:":KYL=7:KYTF=PC(N(PL)):KYF=14:KYB=1:KYFSTR$="\     \":KYA=2:KYY=B1+1:GOSUB 9100:IF KY$=CHR$(27) OR VAL(K$)=0 THEN 11680
12000 IF VAL(K$)>W(N(GV)) THEN GOSUB 11280:GOTO 11990
12010 PA=-VAL(K$):PB=VAL(K$):AC2=RV:AC1=PL:GOSUB 6590:IF NM=0 THEN 1940 ELSE 11650
12020 REM
12030 REM Show unowned properties
12040 LOCATE B2+1,L2+3
12050 FOR UOP=1 TO 39:IF K(UOP)<2 THEN 12100
12060 IF O(UOP) THEN COLOR PC(O(UOP)),0:PRINT CHR$(22);:GOTO 12100
12070 IF GN(UOP)=10 THEN COLOR 7,0:PRINT "Û";:GOTO 12100
12080 IF GN(UOP)=9 THEN COLOR 7,0:PRINT "X";:GOTO 12100
12090 COLOR FC(UOP),BC(UOP):PRINT "±";
12100 NEXT UOP:RETURN
12110 REM
12120 REM CHECK RV
12121 IF NUM_OF_PLAYERS=1 THEN RV=GV ELSE IF RV=0 OR RV=NUM_OF_PLAYERS+1 OR RV=GV THEN RV=GV+1:IF RV>NUM_OF_PLAYERS THEN RV=1
12122 RETURN
12123 REM CALCULATE RV in NM
12124 IF NM=3 OR NM=5 AND PLB<>0 THEN RV=PL2
12125 RETURN
12130 REM
12140 REM change player's name
12141 GOSUB 12162:L=EPN:IF L=0 THEN 10830
12150 K$=J$(N(L)):KYT$="Player "+RIGHT$(STR$(L),1)+"'s name:":KYL=9:KYTF=7:KYF=14:KYB=1:KYFSTR$="\       \":KYA=1:KYY=B1+1:GOSUB 9100
12160 IF KY$=CHR$(27) THEN 10830 ELSE GOSUB 9500:J$(N(L))=K$:GOSUB 1510:GOTO 10830
12161 REM
12162 REM Enter player num
12170 COLOR 7,0:LOCATE B1+1,L2+2:PRINT USING "Enter Player's Number [1-#]";NUM_OF_PLAYERS
12180 EP$=INKEY$:IF LEN(EP$)<>1 THEN 12180
12190 IF EP$=CHR$(27) THEN EPN=0:GOSUB 7330:RETURN ELSE EPN=VAL(EP$)
12200 IF EPN>0 AND EPN<=NUM_OF_PLAYERS THEN GOSUB 7330:RETURN ELSE GOSUB 11280:GOTO 12180
